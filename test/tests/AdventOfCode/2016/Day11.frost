-*

--- Day 11: Radioisotope Thermoelectric Generators ---

You come upon a column of four floors that have been entirely sealed off from the rest of the
building except for a small dedicated lobby. There are some radiation warnings and a big sign which
reads "Radioisotope Testing Facility".

According to the project status board, this facility is currently being used to experiment with
Radioisotope Thermoelectric Generators (RTGs, or simply "generators") that are designed to be paired
with specially-constructed microchips. Basically, an RTG is a highly radioactive rock that generates
electricity through heat.

The experimental RTGs have poor radiation containment, so they're dangerously radioactive. The chips
are prototypes and don't have normal radiation shielding, but they do have the ability to generate
an electromagnetic radiation shield when powered. Unfortunately, they can only be powered by their
corresponding RTG. An RTG powering a microchip is still dangerous to other microchips.

In other words, if a chip is ever left in the same area as another RTG, and it's not connected to
its own RTG, the chip will be fried. Therefore, it is assumed that you will follow procedure and
keep chips connected to their corresponding RTG when they're in the same room, and away from other
RTGs otherwise.

These microchips sound very interesting and useful to your current activities, and you'd like to try
to retrieve them. The fourth floor of the facility has an assembling machine which can make a
self-contained, shielded computer for you to take with you - that is, if you can bring it all of the
RTGs and microchips.

Within the radiation-shielded part of the facility (in which it's safe to have these pre-assembly
RTGs), there is an elevator that can move between the four floors. Its capacity rating means it can
carry at most yourself and two RTGs or microchips in any combination. (They're rigged to some heavy
diagnostic equipment - the assembling machine will detach it for you.) As a security measure, the
elevator will only function if it contains at least one RTG or microchip. The elevator always stops
on each floor to recharge, and this takes long enough that the items within it and the items on that
floor can irradiate each other. (You can prevent this if a Microchip and its Generator end up on the
same floor in this way, as they can be connected while the elevator is recharging.)

You make some notes of the locations of each component of interest (your puzzle input). Before you
don a hazmat suit and start moving things around, you'd like to have an idea of what you need to do.

When you enter the containment area, you and the elevator will start on the first floor.

For example, suppose the isolated area has the following arrangement:

The first floor contains a hydrogen-compatible microchip and a lithium-compatible microchip.
The second floor contains a hydrogen generator.
The third floor contains a lithium generator.
The fourth floor contains nothing relevant.

As a diagram (F# for a Floor number, E for Elevator, H for Hydrogen, L for Lithium, M for Microchip,
and G for Generator), the initial state looks like this:
F4 .  .  .  .  .  
F3 .  .  .  LG .  
F2 .  HG .  .  .  
F1 E  .  HM .  LM 

Then, to get everything up to the assembling machine on the fourth floor, the following steps could
be taken:

Bring the Hydrogen-compatible Microchip to the second floor, which is safe because it can get power
from the Hydrogen Generator:
F4 .  .  .  .  .  
F3 .  .  .  LG .  
F2 E  HG HM .  .  
F1 .  .  .  .  LM 

Bring both Hydrogen-related items to the third floor, which is safe because the Hydrogen-compatible
microchip is getting power from its generator:
F4 .  .  .  .  .  
F3 E  HG HM LG .  
F2 .  .  .  .  .  
F1 .  .  .  .  LM 

Leave the Hydrogen Generator on floor three, but bring the Hydrogen-compatible Microchip back down
with you so you can still use the elevator:
F4 .  .  .  .  .  
F3 .  HG .  LG .  
F2 E  .  HM .  .  
F1 .  .  .  .  LM 

At the first floor, grab the Lithium-compatible Microchip, which is safe because Microchips don't
affect each other:

F4 .  .  .  .  .  
F3 .  HG .  LG .  
F2 .  .  .  .  .  
F1 E  .  HM .  LM 

Bring both Microchips up one floor, where there is nothing to fry them:
F4 .  .  .  .  .  
F3 .  HG .  LG .  
F2 E  .  HM .  LM 
F1 .  .  .  .  .  

Bring both Microchips up again to floor three, where they can be temporarily connected to their
corresponding generators while the elevator recharges, preventing either of them from being fried:
F4 .  .  .  .  .  
F3 E  HG HM LG LM 
F2 .  .  .  .  .  
F1 .  .  .  .  .  

Bring both Microchips to the fourth floor:
F4 E  .  HM .  LM 
F3 .  HG .  LG .  
F2 .  .  .  .  .  
F1 .  .  .  .  .  

Leave the Lithium-compatible microchip on the fourth floor, but bring the Hydrogen-compatible one so
you can still use the elevator; this is safe because although the Lithium Generator is on the
destination floor, you can connect Hydrogen-compatible microchip to the Hydrogen Generator there:
F4 .  .  .  .  LM 
F3 E  HG HM LG .  
F2 .  .  .  .  .  
F1 .  .  .  .  .  

Bring both Generators up to the fourth floor, which is safe because you can connect the
Lithium-compatible Microchip to the Lithium Generator upon arrival:
F4 E  HG .  LG LM 
F3 .  .  HM .  .  
F2 .  .  .  .  .  
F1 .  .  .  .  .  

Bring the Lithium Microchip with you to the third floor so you can use the elevator:
F4 .  HG .  LG .  
F3 E  .  HM .  LM 
F2 .  .  .  .  .  
F1 .  .  .  .  .  

Bring both Microchips to the fourth floor:
F4 E  HG HM LG LM 
F3 .  .  .  .  .  
F2 .  .  .  .  .  
F1 .  .  .  .  .  
In this arrangement, it takes 11 steps to collect all of the objects at the fourth floor for
assembly. (Each elevator stop counts as one step, even if nothing is added to or removed from it.)

In your situation, what is the minimum number of steps required to bring all of the objects to the
fourth floor?

*-

choice Thing {
    RTG(String)
    CHIP(String)

    @override
    function get_toString():String {
        match self {
            when Thing.RTG(name) {
                return name + "_g"
            }
            when Thing.CHIP(name) {
                return name + "_c"
            }
        }
        unreachable
    }
}

class State {
    def elevator:Int
    def floors:Array<Array<Thing>>
    def from:State?

    init(elevator:Int, floors:Array<Array<Thing>>, from:State?) {
        self.elevator := elevator
        self.floors := floors
        self.from := from
    }

    @override
    function get_toString():String {
        return "<" + elevator + ", " + floors + ">"
    }

    var key := "<not validated>"

    method validate():Bit {
        def key := MutableString("<")
        key.append(elevator)
        for f in floors {
            def copy := Array<Thing>(f)
            for t in f {
                match t {
                    when Thing.RTG(rtgName) {
                        for i in 0 .. copy.count {
                            match copy[i] {
                                when Thing.CHIP(chipName) {
                                    if rtgName = chipName {
                                        copy.removeIndex(i)
                                        break
                                    }
                                }
                            }
                        }
                    }
                }
            }
            if copy.toString.contains("_g") & copy.toString.contains("_c") {
                return false
            }
            key.append("|")
            key.append(f.join(","))
        }
        key.append(">")
        self.key := key.finish()
        def seen := Array<String>()
        def matcher := /(\w+)(_[gc])/.matcher(self.key)
        def final := MutableString()
        while matcher.find() {
            var found := -1
            for i in 0 .. seen.count {
                if seen[i] = matcher.group(1) {
                    found := i
                    break
                }
            }
            if found = -1 {
                found := seen.count
                seen.add(matcher.group(1))
            }
            matcher.appendReplacement(final, found.toString + matcher.group(2))
        }
        matcher.appendTail(final)
        self.key := final.finish()
        return true
    }
}

class Day11 {
    def states := Array<State>()
    def visited := HashSet<String>()

    method tryMove(from:Int, to:Int, carrying:ListView<Thing>, state:State) {
        def newState := State(to, state.floors.map(f => Array<Thing>(f)), state)
        for c in carrying {
            newState.floors[from].filterInPlace(t => t != c)
        }
        newState.floors[to].addAll(carrying)
        newState.floors[to].sortInPlace((a, b) => a.toString > b.toString)
        if newState.validate() & !visited.contains(newState.key) {
            states.add(newState)
            visited.add(newState.key)
        }
    }

    method tryMoves(state:State) {
        for i in 1 ... 2 {
            if state.floors[state.elevator].count >= i {
                for c in state.floors[state.elevator].combinations(i) {
                    if state.elevator > 0 {
                        tryMove(state.elevator, state.elevator - 1, c, state)
                    }
                    if state.elevator < state.floors.count - 1{
                        tryMove(state.elevator, state.elevator + 1, c, state)
                    }
                }
            }
        }
    }

    method run(startState:State) {
        states.add(startState)
        var count := 0
        loop {
            count += 1
            Console.printLine(count + " (" + states.count + ")")
            def oldStates := states
            states := Array<State>()
            for s in oldStates {
                tryMoves(s)
            }
            if states.filter(s => s.floors[0 ... 2].map(f => f.count).fold(Int.+) = 0).count > 0 {
                Console.printLine(count)
                var s:State? := states.filter(s => s.floors[0 ... 2].map(f => f.count).fold(Int.+) = 0)[0]
                while s !== null {
                    Console.printLine(s)
                    s := s.from
                }
                break
            }
        }
    }

    @class
    method main() {
    -*
        def floors := Array<Array<Thing>>()
        def lines := File("../test/tests/AdventOfCode/2016/Day11.in").lines().get()
        for i in 0 .. 4 {
            def line := lines.next()
            def floor := Array<Thing>()
            floor.addAll(line.find(/(\w+) generator/).map(m => Thing.RTG(m.groups[1])).all())
            floor.addAll(line.find(/(\w+)-compatible/).map(m => Thing.CHIP(m.groups[1])).all())
            floor.sortInPlace((a, b) => a.toString > b.toString)
            floors.add(floor)
        }
        def startState := State(0, floors, null)
        Day11().run(startState)
        startState.floors[0].addAll([Thing.CHIP("elerium"), Thing.CHIP("dilithium"),
                Thing.RTG("elerium"), Thing.RTG("dilithium")])
        Day11().run(startState)*-
    }
}