-*

--- Day 4: Security Through Obscurity ---

Finally, you come across an information kiosk with a list of rooms. Of course, the list is encrypted
and full of decoy data, but the instructions to decode the list are barely hidden nearby. Better
remove the decoy data first.

Each room consists of an encrypted name (lowercase letters separated by dashes) followed by a dash,
a sector ID, and a checksum in square brackets.

A room is real (not a decoy) if the checksum is the five most common letters in the encrypted name,
in order, with ties broken by alphabetization. For example:

aaaaa-bbb-z-y-x-123[abxyz] is a real room because the most common letters are a (5), b (3), and then
a tie between x, y, and z, which are listed alphabetically.
a-b-c-d-e-f-g-h-987[abcde] is a real room because although the letters are all tied (1 of each), the
first five are listed alphabetically.
not-a-real-room-404[oarel] is a real room.
totally-real-room-200[decoy] is not.

Of the real rooms from the list above, the sum of their sector IDs is 1514.

What is the sum of the sector IDs of the real rooms?

--- Part Two ---

With all the decoy data out of the way, it's time to decrypt this list and get moving.

The room names are encrypted by a state-of-the-art shift cipher, which is nearly unbreakable without
the right software. However, the information kiosk designers at Easter Bunny HQ were not expecting
to deal with a master cryptographer like yourself.

To decrypt a room name, rotate each letter forward through the alphabet a number of times equal to
the room's sector ID. A becomes B, B becomes C, Z becomes A, and so on. Dashes become spaces.

For example, the real name for qzmt-zixmtkozy-ivhz-343 is "very encrypted name".

What is the sector ID of the room where North Pole objects are stored?

*-

function counts(s:String):Array<(Char8, Int)> {
    def result := Array<Int>(26, 0)
    for c in s {
        if c >= "a" & c <= "z" {
            result[c.asUInt8 - "a"->Char8.asUInt8] += 1
        }
    }
    return result.enumeration.map(pair => (Char8("a"->Char8.asUInt8 + pair[0].asUInt8),
            pair[1])).all().sort((pair1, pair2) => pair1[0] > pair2[0]).sort(
                (pair1, pair2) => pair1[1] < pair2[1])
}

function shift(s:String, count:Int):String {
    def a := "a"->Char8.asInt
    return s.utf8.map(c => (c != "-").choose(Char8(((c.asInt + count - a) % 26 + a).asUInt8),
            " ")).join("")
}

method main() {
    def input := Console.inputStream().lines().all()
    def parsed := input.map(line => line.parse(/([\w-]+)-(\d+)\[(\w+)\]/)!)
    def real := parsed.filter(p => counts(p[0])[0 .. 5].map(pair => pair[0]).join("") = p[2])
    Console.printLine(real.map(p => p[1].asInt!).fold(Int.+))
    Console.printLine(real.filter(p => shift(p[0], p[1].asInt!) = "northpole object storage")[0][1])
}