-*

--- Day 23: Safe Cracking ---

This is one of the top floors of the nicest tower in EBHQ. The Easter Bunny's private office is
here, complete with a safe hidden behind a painting, and who wouldn't hide a star in a safe behind a
painting?

The safe has a digital screen and keypad for code entry. A sticky note attached to the safe has a
password hint on it: "eggs". The painting is of a large rabbit coloring some eggs. You see 7.

When you go to type the code, though, nothing appears on the display; instead, the keypad comes
apart in your hands, apparently having been smashed. Behind it is some kind of socket - one that
matches a connector in your prototype computer! You pull apart the smashed keypad and extract the
logic circuit, plug it into your computer, and plug your computer into the safe.

Now, you just need to figure out what output the keypad would have sent to the safe. You extract the
assembunny code from the logic chip (your puzzle input).

The code looks like it uses almost the same architecture and instruction set that the monorail
computer used! You should be able to use the same assembunny interpreter for this as you did there,
but with one new instruction:

tgl x toggles the instruction x away (pointing at instructions like jnz does: positive means
forward; negative means backward):

For one-argument instructions, inc becomes dec, and all other one-argument instructions become inc.
For two-argument instructions, jnz becomes cpy, and all other two-instructions become jnz.
The arguments of a toggled instruction are not affected.
If an attempt is made to toggle an instruction outside the program, nothing happens.
If toggling produces an invalid instruction (like cpy 1 2) and an attempt is later made to execute
that instruction, skip it instead.
If tgl toggles itself (for example, if a is 0, tgl a would target itself and become inc a), the
resulting instruction is not executed until the next time it is reached.

For example, given this program:

cpy 2 a
tgl a
tgl a
tgl a
cpy 1 a
dec a
dec a

cpy 2 a initializes register a to 2.

The first tgl a toggles an instruction a (2) away from it, which changes the third tgl a into inc a.

The second tgl a also modifies an instruction 2 away from it, which changes the cpy 1 a into jnz 1
a.

The fourth line, which is now inc a, increments a to 3.

Finally, the fifth line, which is now jnz 1 a, jumps a (3) instructions ahead, skipping the dec a
instructions.

In this example, the final value in register a is 3.

The rest of the electronics seem to place the keypad entry (the number of eggs, 7) in register a,
run the code, and then send the value left in register a to the safe.

What value should be sent to the safe?

--- Part Two ---

The safe doesn't open, but it does make several angry noises to express its frustration.

You're quite sure your logic is working correctly, so the only other thing is... you check the
painting again. As it turns out, colored eggs are still eggs. Now you count 12.

As you run the program with this new input, the prototype computer begins to overheat. You wonder
what's taking so long, and whether the lack of any instruction more powerful than "add one" has
anything to do with it. Don't bunnies usually multiply?

Anyway, what value should actually be sent to the safe?

*-

choice Value {
    IMMEDIATE(Int)
    REGISTER(Int)
}

choice Instruction {
    CPY(Value, Value)
    INC(Value)
    DEC(Value)
    JNZ(Value, Value)
    JMP(Value)
    TGL(Value)
}

method run(code:Array<Instruction>, registers:Array<Int>) {
    function get(v:Value):Int {
        match v {
            when Value.IMMEDIATE(x) { return x }
            when Value.REGISTER(x) { return registers[x] }
        }
    }
    method set(dst:Value, v:Int) {
        match dst {
            when Value.IMMEDIATE(x) { return }
            when Value.REGISTER(x) { registers[x] := v }
        }
    }
    var ip := 0
    while ip < code.count {
        match code[ip] {
            when Instruction.CPY(x, y) { set(y, get(x)) }
            when Instruction.INC(x)    { set(x, get(x) + 1) }
            when Instruction.DEC(x)    { set(x, get(x) - 1) }
            when Instruction.JNZ(x, y) { if get(x) != 0 { ip += get(y) - 1 } }
            when Instruction.JMP(x)    { ip += get(x) - 1 }
            when Instruction.TGL(x) {
                def target := ip + get(x)
                if target >= 0 & target < code.count {
                    match code[target] {
                        when Instruction.CPY(x, y)   { code[target] := Instruction.JNZ(x, y) }
                        when Instruction.INC(x)      { code[target] := Instruction.DEC(x) }
                        when Instruction.DEC(x)      { code[target] := Instruction.INC(x) }
                        when Instruction.JNZ(x, y)   { code[target] := Instruction.CPY(x, y) }
                        when Instruction.JMP(x)      { code[target] := Instruction.INC(x) }
                        when Instruction.TGL(x)      { code[target] := Instruction.INC(x) }
                    }
                }
            }
        }
        ip += 1
    }
    Console.printLine(registers[0])
}

method main() {
    function value(s:String):Value {
        match s {
            when "a" { return Value.REGISTER(0) }
            when "b" { return Value.REGISTER(1) }
            when "c" { return Value.REGISTER(2) }
            when "d" { return Value.REGISTER(3) }
            otherwise { return Value.IMMEDIATE(s.asInt!) }
        }
    }
    def code := Array<Instruction>()
    for line in Console.inputStream().lines() {
        def components := line.split(" ")
        match components[0] {
            when "cpy" { code.add(Instruction.CPY(value(components[1]), value(components[2]))) }
            when "inc" { code.add(Instruction.INC(value(components[1]))) }
            when "dec" { code.add(Instruction.DEC(value(components[1]))) }
            when "tgl" { code.add(Instruction.TGL(value(components[1]))) }
            when "jnz" { code.add(Instruction.JNZ(value(components[1]), value(components[2]))) }
        }
    }
    run(Array<Instruction>(code), [7, 0, 0, 0])
    -- Part 2 is not reasonably brute forcable
}