-*

--- Day 24: Electromagnetic Moat ---

The CPU itself is a large, black building surrounded by a bottomless pit. Enormous metal tubes
extend outward from the side of the building at regular intervals and descend down into the void.
There's no way to cross, but you need to get inside.

No way, of course, other than building a bridge out of the magnetic components strewn about nearby.

Each component has two ports, one on each end. The ports come in all different types, and only
matching types can be connected. You take an inventory of the components by their port types (your
puzzle input). Each port is identified by the number of pins it uses; more pins mean a stronger
connection for your bridge. A 3/7 component, for example, has a type-3 port on one side, and a
type-7 port on the other.

Your side of the pit is metallic; a perfect surface to connect a magnetic, zero-pin port. Because of
this, the first port you use must be of type 0. It doesn't matter what type of port you end with;
your goal is just to make the bridge as strong as possible.

The strength of a bridge is the sum of the port types in each component. For example, if your bridge
is made of components 0/3, 3/7, and 7/4, your bridge has a strength of 0+3 + 3+7 + 7+4 = 24.

For example, suppose you had the following components:

0/2
2/2
2/3
3/4
3/5
0/1
10/1
9/10

With them, you could make the following valid bridges:

0/1
0/1--10/1
0/1--10/1--9/10
0/2
0/2--2/3
0/2--2/3--3/4
0/2--2/3--3/5
0/2--2/2
0/2--2/2--2/3
0/2--2/2--2/3--3/4
0/2--2/2--2/3--3/5

(Note how, as shown by 10/1, order of ports within a component doesn't matter. However, you may only
use each port on a component once.)

Of these bridges, the strongest one is 0/1--10/1--9/10; it has a strength of 0+1 + 1+10 + 10+9 = 31.

What is the strength of the strongest bridge you can make with the components you have available?

--- Part Two ---

The bridge you've built isn't long enough; you can't jump the rest of the way.

In the example above, there are two longest bridges:

0/2--2/2--2/3--3/4
0/2--2/2--2/3--3/5
Of them, the one which uses the 3/5 component is stronger; its strength is
0+2 + 2+2 + 2+3 + 3+5 = 19.

What is the strength of the longest bridge you can make? If you can make multiple bridges of the
longest length, pick the strongest one.

*-

method find(start:Int, strength:Int, components:Array<Array<Int>>, endpoints:Array<Array<Int>>,
        used:HashSet<Int>):(Int, (Int, Int)) {
    var strongest := strength
    var longest := (used.count, strength)
    for c in endpoints[start] {
        if used.contains(c) {
            continue
        }
        used.add(c)
        var end:Int
        if components[c][0] = start {
            end := components[c][1]
        }
        else {
            end := components[c][0]
        }
        def result := find(end, strength + start + end, components, endpoints, used)
        strongest := strongest.max(result[0])
        if result[1][0] > longest[0] | (result[1][0] = longest[0] & result[1][1] > longest[1]) {
            longest := result[1]
        }
        used.remove(c)
    }
    return (strongest, longest)
}

method main() {
    def components := Console.inputStream().lines().map(s => s.split("/").map(n => n.asInt!)).all()
    def endpoints := Array<Array<Int>>()
    method add(endpoint:Int, component:Int) {
        while endpoints.count <= endpoint {
            endpoints.add(Array<Int>())
        }
        endpoints[endpoint].add(component)
    }
    for (i, c) in components.enumeration {
        add(c[0], i)
        if c[0] != c[1] {
            add(c[1], i)
        }
    }
    def result := find(0, 0, components, endpoints, HashSet<Int>())
    Console.printLine(result[0])
    Console.printLine(result[1][1])
}