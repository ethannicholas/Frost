-*

--- Day 20: A Regular Map ---

While you were learning about instruction pointers, the Elves made considerable progress. When you
look up, you discover that the North Pole base construction project has completely surrounded you.

The area you are in is made up entirely of rooms and doors. The rooms are arranged in a grid, and
rooms only connect to adjacent rooms when a door is present between them.

For example, drawing rooms as ., walls as #, doors as | or -, your current position as X, and where
north is up, the area you're in might look like this:

#####
#.|.#
#-###
#.|X#
#####

You get the attention of a passing construction Elf and ask for a map. "I don't have time to draw
out a map of this place - it's huge. Instead, I can give you directions to every room in the
facility!" He writes down some directions on a piece of parchment and runs off. In the example
above, the instructions might have been ^WNE$, a regular expression or "regex" (your puzzle input).

The regex matches routes (like WNE for "west, north, east") that will take you from your current
room through various doors in the facility. In aggregate, the routes will take you through every
door in the facility at least once; mapping out all of these routes will let you build a proper map
and find your way around.

'^' and '$' are at the beginning and end of your regex; these just mean that the regex doesn't match
anything outside the routes it describes. (Specifically, '^' matches the start of the route, and '$'
matches the end of it.) These characters will not appear elsewhere in the regex.

The rest of the regex matches various sequences of the characters N (north), S (south), E (east),
and W (west). In the example above, ^WNE$ matches only one route, WNE, which means you can move
west, then north, then east from your current position. Sequences of letters like this always match
that exact route in the same order.

Sometimes, the route can branch. A branch is given by a list of options separated by pipes (|) and
wrapped in parentheses. So, ^N(E|W)N$ contains a branch: after going north, you must choose to go
either east or west before finishing your route by going north again. By tracing out the possible
routes after branching, you can determine where the doors are and, therefore, where the rooms are in
the facility.

For example, consider this regex: ^ENWWW(NEEE|SSE(EE|N))$

This regex begins with ENWWW, which means that from your current position, all routes must begin by
moving east, north, and then west three times, in that order. After this, there is a branch. Before
you consider the branch, this is what you know about the map so far, with doors you aren't sure
about marked with a ?:

#?#?#?#?#
?.|.|.|.?
#?#?#?#-#
    ?X|.?
    #?#?#

After this point, there is (NEEE|SSE(EE|N)). This gives you exactly two options: NEEE and SSE(EE|N).
 By following NEEE, the map now looks like this:

#?#?#?#?#
?.|.|.|.?
#-#?#?#?#
?.|.|.|.?
#?#?#?#-#
    ?X|.?
    #?#?#

Now, only SSE(EE|N) remains. Because it is in the same parenthesized group as NEEE, it starts from
the same room NEEE started in. It states that starting from that point, there exist doors which will
allow you to move south twice, then east; this ends up at another branch. After that, you can either
move east twice or north once. This information fills in the rest of the doors:

#?#?#?#?#
?.|.|.|.?
#-#?#?#?#
?.|.|.|.?
#-#?#?#-#
?.?.?X|.?
#-#-#?#?#
?.|.|.|.?
#?#?#?#?#

Once you've followed all possible routes, you know the remaining unknown parts are all walls,
producing a finished map of the facility:

#########
#.|.|.|.#
#-#######
#.|.|.|.#
#-#####-#
#.#.#X|.#
#-#-#####
#.|.|.|.#
#########

Sometimes, a list of options can have an empty option, like (NEWS|WNSE|). This means that routes at
this point could effectively skip the options in parentheses and move on immediately. For example,
consider this regex and the corresponding map:

^ENNWSWW(NEWS|)SSSEEN(WNSE|)EE(SWEN|)NNN$

###########
#.|.#.|.#.#
#-###-#-#-#
#.|.|.#.#.#
#-#####-#-#
#.#.#X|.#.#
#-#-#####-#
#.#.|.|.|.#
#-###-###-#
#.|.|.#.|.#
###########

This regex has one main route which, at three locations, can optionally include additional detours
and be valid: (NEWS|), (WNSE|), and (SWEN|). Regardless of which option is taken, the route
continues from the position it is left at after taking those steps. So, for example, this regex
matches all of the following routes (and more that aren't listed here):

ENNWSWWSSSEENEENNN
ENNWSWWNEWSSSSEENEENNN
ENNWSWWNEWSSSSEENEESWENNNN
ENNWSWWSSSEENWNSEEENNN

By following the various routes the regex matches, a full map of all of the doors and rooms in the
facility can be assembled.

To get a sense for the size of this facility, you'd like to determine which room is furthest from
you: specifically, you would like to find the room for which the shortest path to that room would
require passing through the most doors.

In the first example (^WNE$), this would be the north-east corner 3 doors away.
In the second example (^ENWWW(NEEE|SSE(EE|N))$), this would be the south-east corner 10 doors away.
In the third example (^ENNWSWW(NEWS|)SSSEEN(WNSE|)EE(SWEN|)NNN$), this would be the north-east
corner 18 doors away.

Here are a few more examples:

Regex: ^ESSWWN(E|NNENN(EESS(WNSE|)SSS|WWWSSSSE(SW|NNNE)))$
Furthest room requires passing 23 doors

#############
#.|.|.|.|.|.#
#-#####-###-#
#.#.|.#.#.#.#
#-#-###-#-#-#
#.#.#.|.#.|.#
#-#-#-#####-#
#.#.#.#X|.#.#
#-#-#-###-#-#
#.|.#.|.#.#.#
###-#-###-#-#
#.|.#.|.|.#.#
#############

Regex: ^WSSEESWWWNW(S|NENNEEEENN(ESSSSW(NWSW|SSEN)|WSWWN(E|WWS(E|SS))))$
Furthest room requires passing 31 doors

###############
#.|.|.|.#.|.|.#
#-###-###-#-#-#
#.|.#.|.|.#.#.#
#-#########-#-#
#.#.|.|.|.|.#.#
#-#-#########-#
#.#.#.|X#.|.#.#
###-#-###-#-#-#
#.|.#.#.|.#.|.#
#-###-#####-###
#.|.#.|.|.#.#.#
#-#-#####-#-#-#
#.#.|.|.|.#.|.#
###############

What is the largest number of doors you would be required to pass through to reach a room? That is,
find the room for which the shortest path from your starting location to that room would require
passing through the most doors; what is the fewest doors you can pass through to reach it?

--- Part Two ---

Okay, so the facility is big.

How many rooms have a shortest path from your current location that pass through at least 1000
doors?

*-

method dump(tiles:HashMap<(Int, Int), Char8>) {
    def all := tiles.keys.all()
    def minX := all.map(c => c[0]).fold(Int.min)
    def maxX := all.map(c => c[0]).fold(Int.max)
    def minY := all.map(c => c[1]).fold(Int.min)
    def maxY := all.map(c => c[1]).fold(Int.max)
    for y in minY - 1 ... maxY + 1 {
        for x in minX - 1 ... maxX + 1 {
            def c := tiles[(x, y)]
            if c !== null {
                Console.print(c)
            }
            else {
                Console.print("#")
            }
        }
        Console.printLine()
    }
}

def part2 := HashSet<(Int, Int)>()

method longest(tiles:HashMap<(Int, Int), Char8>, start:(Int, Int),
        visited:HashSet<(Int, Int)>):Int {
    function isDoor(x:Int, y:Int):Bit {
        def c := tiles[(x, y)]
        return c !== null & (c = "|" | c = "-")
    }
    visited.add(start)
    var result := visited.count - 1
    if result >= 1000 {
        part2.add(start)
    }
    if !visited.contains((start[0], start[1] - 2)) & isDoor(start[0], start[1] - 1) {
        result := result.max(longest(tiles, (start[0], start[1] - 2), visited))
    }
    if !visited.contains((start[0] - 2, start[1])) & isDoor(start[0] - 1, start[1]) {
        result := result.max(longest(tiles, (start[0] - 2, start[1]), visited))
    }
    if !visited.contains((start[0] + 2, start[1])) & isDoor(start[0] + 1, start[1]) {
        result := result.max(longest(tiles, (start[0] + 2, start[1]), visited))
    }
    if !visited.contains((start[0], start[1] + 2)) & isDoor(start[0], start[1] + 1) {
        result := result.max(longest(tiles, (start[0], start[1] + 2), visited))
    }
    visited.remove(start)
    return result
}

method main() {
    def regex := Console.inputStream().readFully()
    def tiles := HashMap<(Int, Int), Char8>()
    tiles[(0, 0)] := "X"
    var start := Stack<(Int, Int)>()
    start.push((0, 0))
    var current := (0, 0)
    for c in regex {
        match c {
            when "^", "$" {}
            when "N" {
                tiles[(current[0], current[1] - 1)] := "-"
                tiles[(current[0], current[1] - 2)] := "."
                current := (current[0], current[1] - 2)
            }
            when "W" {
                tiles[(current[0] - 1, current[1])] := "|"
                tiles[(current[0] - 2, current[1])] := "."
                current := (current[0] - 2, current[1])
            }
            when "E" {
                tiles[(current[0] + 1, current[1])] := "|"
                tiles[(current[0] + 2, current[1])] := "."
                current := (current[0] + 2, current[1])
            }
            when "S" {
                tiles[(current[0], current[1] + 1)] := "-"
                tiles[(current[0], current[1] + 2)] := "."
                current := (current[0], current[1] + 2)
            }
            when "(" {
                start.push(current)
            }
            when ")" {
                start.pop()
            }
            when "|" {
                current := start[0]
            }
        }
    }
    Console.printLine(longest(tiles, (0, 0), HashSet<(Int, Int)>()))
    Console.printLine(part2.count)
}