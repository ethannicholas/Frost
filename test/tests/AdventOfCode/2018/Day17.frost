-*

--- Day 17: Reservoir Research ---

You arrive in the year 18. If it weren't for the coat you got in 1018, you would be very cold: the
North Pole base hasn't even been constructed.

Rather, it hasn't been constructed yet. The Elves are making a little progress, but there's not a
lot of liquid water in this climate, so they're getting very dehydrated. Maybe there's more
underground?

You scan a two-dimensional vertical slice of the ground nearby and discover that it is mostly sand
with veins of clay. The scan only provides data with a granularity of square meters, but it should
be good enough to determine how much water is trapped there. In the scan, x represents the distance
to the right, and y represents the distance down. There is also a spring of water near the surface
at x=500, y=0. The scan identifies which square meters are clay (your puzzle input).

For example, suppose your scan shows the following veins of clay:

x=495, y=2..7
y=7, x=495..501
x=501, y=3..7
x=498, y=2..4
x=506, y=1..2
x=498, y=10..13
x=504, y=10..13
y=13, x=498..504

Rendering clay as '#', sand as '.', and the water spring as '+', and with x increasing to the right
and y increasing downward, this becomes:

   44444455555555
   99999900000000
   45678901234567
 0 ......+.......
 1 ............#.
 2 .#..#.......#.
 3 .#..#..#......
 4 .#..#..#......
 5 .#.....#......
 6 .#.....#......
 7 .#######......
 8 ..............
 9 ..............
10 ....#.....#...
11 ....#.....#...
12 ....#.....#...
13 ....#######...

The spring of water will produce water forever. Water can move through sand, but is blocked by clay.
Water always moves down when possible, and spreads to the left and right otherwise, filling space
that has clay on both sides and falling out otherwise.

For example, if five squares of water are created, they will flow downward until they reach the clay
and settle there. Water that has come to rest is shown here as '~', while sand through which water
has passed (but which is now dry again) is shown as '|':

......+.......
......|.....#.
.#..#.|.....#.
.#..#.|#......
.#..#.|#......
.#....|#......
.#~~~~~#......
.#######......
..............
..............
....#.....#...
....#.....#...
....#.....#...
....#######...

Two squares of water can't occupy the same location. If another five squares of water are created,
they will settle on the first five, filling the clay reservoir a little more:

......+.......
......|.....#.
.#..#.|.....#.
.#..#.|#......
.#..#.|#......
.#~~~~~#......
.#~~~~~#......
.#######......
..............
..............
....#.....#...
....#.....#...
....#.....#...
....#######...

Water pressure does not apply in this scenario. If another four squares of water are created, they
will stay on the right side of the barrier, and no water will reach the left side:

......+.......
......|.....#.
.#..#.|.....#.
.#..#~~#......
.#..#~~#......
.#~~~~~#......
.#~~~~~#......
.#######......
..............
..............
....#.....#...
....#.....#...
....#.....#...
....#######...

At this point, the top reservoir overflows. While water can reach the tiles above the surface of the
water, it cannot settle there, and so the next five squares of water settle like this:

......+.......
......|.....#.
.#..#||||...#.
.#..#~~#|.....
.#..#~~#|.....
.#~~~~~#|.....
.#~~~~~#|.....
.#######|.....
........|.....
........|.....
....#...|.#...
....#...|.#...
....#~~~~~#...
....#######...

Note especially the leftmost '|': the new squares of water can reach this tile, but cannot stop
there. Instead, eventually, they all fall to the right and settle in the reservoir below.

After 10 more squares of water, the bottom reservoir is also full:

......+.......
......|.....#.
.#..#||||...#.
.#..#~~#|.....
.#..#~~#|.....
.#~~~~~#|.....
.#~~~~~#|.....
.#######|.....
........|.....
........|.....
....#~~~~~#...
....#~~~~~#...
....#~~~~~#...
....#######...

Finally, while there is nowhere left for the water to settle, it can reach a few more tiles before
overflowing beyond the bottom of the scanned data:

......+.......    (line not counted: above minimum y value)
......|.....#.
.#..#||||...#.
.#..#~~#|.....
.#..#~~#|.....
.#~~~~~#|.....
.#~~~~~#|.....
.#######|.....
........|.....
...|||||||||..
...|#~~~~~#|..
...|#~~~~~#|..
...|#~~~~~#|..
...|#######|..
...|.......|..    (line not counted: below maximum y value)
...|.......|..    (line not counted: below maximum y value)
...|.......|..    (line not counted: below maximum y value)

How many tiles can be reached by the water? To prevent counting forever, ignore tiles with a y
coordinate smaller than the smallest y coordinate in your scan data or larger than the largest one.
Any x coordinate is valid. In this example, the lowest y coordinate given is 1, and the highest is
13, causing the water spring (in row 0) and the water falling off the bottom of the render (in rows
14 through infinity) to be ignored.

So, in the example above, counting both water at rest (~) and other sand tiles the water can
hypothetically reach (|), the total number of tiles the water can reach is 57.

How many tiles can the water reach within the range of y values in your scan?

--- Part Two ---

After a very long time, the water spring will run dry. How much water will be retained?

In the example above, water that won't eventually drain out is shown as '~', a total of 29 tiles.

How many water tiles are left after the water spring stops producing water and all remaining water
not at rest has drained?

*-

var minX := Int.MAX
var maxX := Int.MIN
var minY := Int.MAX
var maxY := Int.MIN
def tiles := HashMap<(Int, Int), Tile>()
def pending := HashSet<(Int, Int)>()

choice Tile {
    CLAY
    FLOWING
    SETTLED
}

method dump() {
    for y in minY ... maxY {
        for x in minX - 1 ... maxX + 1 {
            def tile := tiles[(x, y)]
            if tile !== null {
                match tile {
                    when Tile.CLAY    { Console.print("#") }
                    when Tile.FLOWING { Console.print("|") }
                    when Tile.SETTLED { Console.print("~") }
                }
            }
            else {
                Console.print(" ")
            }
        }
        Console.printLine()
    }
}

method canFlowTo(x:Int, y:Int):Bit {
    return tiles[(x, y)] == null | tiles[(x, y)] = Tile.FLOWING
}

method flowFrom(x:Int, startY:Int) {
    var y := startY
    while canFlowTo(x, y + 1) & y <= maxY {
        tiles[(x, y)] := Tile.FLOWING
        y += 1
    }
    if y < maxY {
        var left := x
        while !canFlowTo(left, y + 1) & canFlowTo(left - 1, y) {
            left -= 1
        }
        var right := x
        while !canFlowTo(right, y + 1) & canFlowTo(right + 1, y) {
            right += 1
        }
        if canFlowTo(left, y + 1) | canFlowTo(right, y + 1) {
            for x in left ... right {
                tiles[(x, y)] := Tile.FLOWING
            }
            if canFlowTo(left, y + 1) {
                pending.add((left, y + 1))
            }
            if canFlowTo(right, y + 1) {
                pending.add((right, y + 1))
            }
        }
        else {
            for x in left ... right {
                tiles[(x, y)] := Tile.SETTLED
            }
        }
    }
}

method main() {
    for line in Console.inputStream().lines().all() {
        def parsed := line.parse(/([xy])=(\d+), .=(\d+)..(\d+)/)!
        if parsed[0] = "x" {
            def x := parsed[1].asInt!
            for y in parsed[2].asInt! ... parsed[3].asInt! {
                tiles[(x, y)] := Tile.CLAY
            }
            minX := minX.min(x)
            maxX := maxX.max(x)
            minY := minY.min(parsed[2].asInt!)
            maxY := maxY.max(parsed[3].asInt!)
        }
        else {
            def y := parsed[1].asInt!
            for x in parsed[2].asInt! ... parsed[3].asInt! {
                tiles[(x, y)] := Tile.CLAY
            }
            minX := minX.min(parsed[2].asInt!)
            maxX := maxX.max(parsed[3].asInt!)
            minY := minY.min(y)
            maxY := maxY.max(y)
        }
    }
    var lastCount := tiles.count
    loop {
        pending.add((500, 0))
        while pending.count > 0 {
            def next := pending.first
            pending.remove(next)
            flowFrom(next[0], next[1])
        }
        if tiles.count = lastCount {
            break
        }
        lastCount := tiles.count
    }
    Console.printLine(tiles.entries.filter(e => e[0][1] >= self.minY &
            (e[1] = Tile.FLOWING | e[1] = Tile.SETTLED)).count())
    Console.printLine(tiles.values.filter(v => v = Tile.SETTLED).count())
}