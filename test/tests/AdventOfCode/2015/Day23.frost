-*

--- Day 23: Opening the Turing Lock ---

Little Jane Marie just got her very first computer for Christmas from some unknown benefactor. It
comes with instructions and an example program, but the computer itself seems to be malfunctioning.
She's curious what the program does, and would like you to help her run it.

The manual explains that the computer supports two registers and six instructions (truly, it goes on
to remind the reader, a state-of-the-art technology). The registers are named a and b, can hold any
non-negative integer, and begin with a value of 0. The instructions are as follows:

hlf r sets register r to half its current value, then continues with the next instruction.
tpl r sets register r to triple its current value, then continues with the next instruction.
inc r increments register r, adding 1 to it, then continues with the next instruction.
jmp offset is a jump; it continues with the instruction offset away relative to itself.
jie r, offset is like jmp, but only jumps if register r is even ("jump if even").
jio r, offset is like jmp, but only jumps if register r is 1 ("jump if one", not odd).

All three jump instructions work with an offset relative to that instruction. The offset is always
written with a prefix + or - to indicate the direction of the jump (forward or backward,
respectively). For example, jmp +1 would simply continue with the next instruction, while jmp +0
would continuously jump back to itself forever.

The program exits when it tries to run an instruction beyond the ones defined.

For example, this program sets a to 2, because the jio instruction causes it to skip the tpl
instruction:

inc a
jio a, +2
tpl a
inc a

What is the value in register b when the program in your puzzle input is finished executing?

--- Part Two ---

The unknown benefactor is very thankful for releasi-- er, helping little Jane Marie with her
computer. Definitely not to distract you, what is the value in register b after the program is
finished executing if register a starts as 1 instead?

*-

class Day23 {
    def registers:HashMap<String, Int>
    var cmds:ListView<String>

    init(registers:ListView<(String, Int)>, cmds:ListView<String>) {
        self.registers := HashMap<String, Int>(registers)
        self.registers["ip"] := 0
        self.cmds := cmds
    }

    method step() {
        method process(reg:String, f:(Int)=>(Int)) {
            self.registers[reg] := f(self.registers[reg])
        }
        method jump(s:String) {
            process("ip", (s.replace("+", "").asInt - 1).+)    
        }
        def cmd := cmds[registers["ip"]].split(/[\s,]+/)
        match cmd[0] {
            when "hlf" { process(cmd[1], x => x // 2) }
            when "tpl" { process(cmd[1], 3.*) }
            when "inc" { process(cmd[1], 1.+) }
            when "jmp" { jump(cmd[1]) }
            when "jie" { if registers[cmd[1]] % 2 = 0 { jump(cmd[2]) } }
            when "jio" { if registers[cmd[1]] = 1 { jump(cmd[2]) } }
        }
        process("ip", 1.+)
    }

    method run() {
        while registers["ip"] >= 0 & registers["ip"] < cmds.count {
            step()
        }
        Console.printLine(registers["b"])
    }

    @class
    method main() {
        def cmds := Console.inputStream().lines().all()
        Day23([("a", 0), ("b", 0)], cmds).run()
        Day23([("a", 1), ("b", 0)], cmds).run()
    }
}