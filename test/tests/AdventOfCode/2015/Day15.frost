-*

--- Day 15: Science for Hungry People ---

Today, you set out on the task of perfecting your milk-dunking cookie recipe. All you have to do is
find the right balance of ingredients.

Your recipe leaves room for exactly 100 teaspoons of ingredients. You make a list of the remaining
ingredients you could use to finish the recipe (your puzzle input) and their properties per
teaspoon:

capacity (how well it helps the cookie absorb milk)
durability (how well it keeps the cookie intact when full of milk)
flavor (how tasty it makes the cookie)
texture (how it improves the feel of the cookie)
calories (how many calories it adds to the cookie)

You can only measure ingredients in whole-teaspoon amounts accurately, and you have to be accurate
so you can reproduce your results in the future. The total score of a cookie can be found by adding
up each of the properties (negative totals become 0) and then multiplying together everything except
calories.

For instance, suppose you have these two ingredients:

Butterscotch: capacity -1, durability -2, flavor 6, texture 3, calories 8
Cinnamon: capacity 2, durability 3, flavor -2, texture -1, calories 3
Then, choosing to use 44 teaspoons of butterscotch and 56 teaspoons of cinnamon
(because the amounts of each ingredient must add up to 100) would result in a 
cookie with the following properties:

A capacity of 44 * -1 + 56 * 2 = 68
A durability of 44 * -2 + 56 * 3 = 80
A flavor of 44 * 6 + 56 * -2 = 152
A texture of 44 * 3 + 56 * -1 = 76
Multiplying these together (68 * 80 * 152 * 76, ignoring calories for now) 
results in a total score of 62842880, which happens to be the best score 
possible given these ingredients. If any properties had produced a negative 
total, it would have instead become zero, causing the whole score to multiply to
zero.

Given the ingredients in your kitchen and their properties, what is the total
score of the highest-scoring cookie you can make?

--- Part Two ---

Your cookie recipe becomes wildly popular! Someone asks if you can make another recipe that has
exactly 500 calories per cookie (so they can use it as a meal replacement). Keep the rest of your
award-winning process the same (100 teaspoons, same ingredients, same scoring system).

For example, given the ingredients above, if you had instead selected 40 teaspoons of butterscotch
and 60 teaspoons of cinnamon (which still adds to 100), the total calorie count would be 40*8 +
60*3 = 500. The total score would go down, though: only 57600000, the best you can do in such trying
circumstances.

Given the ingredients in your kitchen and their properties, what is the total
score of the highest-scoring cookie you can make with a calorie total of 500?

*-

function bestOverall(total:Int, soFar:ListView<Int>, remaining:Int, 
        f:(ListView<Int>)=>(Int)):Int {
    if remaining > 1 {
        var best := -1
        for i in 0 ... total {
            def list := Array<Int>(soFar)
            list.add(i)
            def current := bestOverall(total - i, list, remaining - 1, f)
            if current > best {
                best := current
            }
        }
        return best
    }
    else {
        def final := Array<Int>(soFar)
        final.add(total)
        return f(final)
    }
}

function score(ingredients:Array<ListView<Int>>, 
        requiredCalories:Int?):(ListView<Int>)=>(Int) {
    return function(amounts:ListView<Int>):Int {
        def score := Array<Int>(5, 0)
        for i in 0 .. amounts.count {
            for j in 0 .. ingredients[i].count {
                score[j] += amounts[i] * ingredients[i][j]
            }
        }
        for i in 0 .. score.count {
            score[i] := score[i].max(0)
        }
        if requiredCalories !== null & score[4] != requiredCalories {
            return 0
        }
        score[4] := 0
        return score[0 .. 4].fold(Int.*)
    }
}

method main() {
    def ingredients := Array<ListView<Int>>()
    for line in Console.inputStream().lines() {
        def parsed := line.split(/[^\d-]+/)
        assert parsed.count = 6
        ingredients.add(parsed[1..].map(p => p.asInt!))
    }
    Console.printLine(bestOverall(100, [], ingredients.count, score(ingredients, 
            null)))
    Console.printLine(bestOverall(100, [], ingredients.count, score(ingredients, 
            500)))
}
