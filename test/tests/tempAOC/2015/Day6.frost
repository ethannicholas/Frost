-*

--- Day 6: Probably a Fire Hazard ---

Because your neighbors keep defeating you in the holiday house decorating contest year after year,
you've decided to deploy one million lights in a 1000x1000 grid.

Furthermore, because you've been especially nice this year, Santa has mailed you instructions on how
to display the ideal lighting configuration.

Lights in your grid are numbered from 0 to 999 in each direction; the lights at each corner are at
0,0, 0,999, 999,999, and 999,0. The instructions include whether to turn on, turn off, or toggle
various inclusive ranges given as oordinate pairs. Each coordinate pair represents opposite corners
of a rectangle, inclusive; a coordinate pair like 0,0 through 2,2 therefore refers to 9 lights in a
3x3 square. The lights all start turned off.

To defeat your neighbors this year, all you have to do is set up your lights by doing the
instructions Santa sent you in order.

For example:

turn on 0,0 through 999,999 would turn on (or leave on) every light.
toggle 0,0 through 999,0 would toggle the first line of 1000 lights, turning off the ones that were
on, and turning on the ones that were off.
turn off 499,499 through 500,500 would turn off (or leave off) the middle four lights.
After following the instructions, how many lights are lit?

--- Part Two ---

You just finish implementing your winning light pattern when you realize you mistranslated Santa's
message from Ancient Nordic Elvish.

The light grid you bought actually has individual brightness controls; each light can have a
brightness of zero or more. The lights all start at zero.

The phrase turn on actually means that you should increase the brightness of those lights by 1.

The phrase turn off actually means that you should decrease the brightness of those lights by 1, to
a minimum of zero.

The phrase toggle actually means that you should increase the brightness of those lights by 2.

What is the total brightness of all lights combined after following Santa's instructions?

For example:

turn on 0,0 through 0,0 would increase the total brightness by 1.
toggle 0,0 through 999,999 would increase the total brightness by 2000000.

*-

method main() {
    def lights := Array<Array<(Bit, Int)>>()
    for i in 0 .. 1000 {
        lights.add(Array<(Bit, Int)>(1000, (false, 0)))
    }
    def regex := /(turn (?:on|off)|toggle) (\d+),(\d+) through (\d+),(\d+)/
    for line in Console.inputStream().lines() {
        def parsed := line.parse(regex)
        assert parsed !== null
        def f:((Bit, Int))=>((Bit, Int))
        match parsed[0] {
            when "turn on"  { f := s => (true, s[1] + 1) }
            when "turn off" { f := s => (false, (s[1] - 1).max(0)) }
            when "toggle"   { f := s => (!s[0], s[1] + 2) }
        }
        for x in parsed[1].asInt64 ... parsed[3].asInt64 {
            for y in parsed[2].asInt64 ... parsed[4].asInt64 {
                lights[x][y] := f(lights[x][y])
            }
        }
    }
    var count := 0
    var total := 0
    for row in lights {
        count += row.filter(s => s[0]).count
        total += row.fold((s1, s2) => (true, s1[1] + s2[1]))[1]
    }
    Console.printLine(count)
    Console.printLine(total)
}