class Test {
    @class
    method runTest(pandac:File, workingDir:File, test:String):Bit {
        Console.print("Testing \{test}...")
        def expected := workingDir.resolve(test + ".expected").readFully()
        def exe := File("/tmp/test.out")
        def args := Array<String>() -- FIXME literal
        args.add(workingDir.resolve(test + ".panda").path)
        args.add("-o")
        args.add(exe.path)
        def compiler := System.exec(pandac, args)
        def compileResult := compiler.output.readFully() -- FIXME use error stream instead
        compiler.waitFor()
        if compileResult != "" {
            if compileResult != expected {
                Console.printLine(compileResult.hash() + " - " + expected.hash())
                Console.printLine("error, expected:\n\{expected}\nbut received:\n\{compileResult}")
                return false
            }
            Console.printLine("success")
            return true
        }
        def child := System.exec(exe, Array<String>())
        def input := workingDir.resolve(test + ".in")
        if input.exists() {
            child.input.print(input.readFully())
            child.input.cleanup()
        }
        def childResult := child.output.readFully()
        child.waitFor()
        if childResult != expected {
            Console.printLine("error, expected:\n\{expected}\nbut received:\n\{childResult}")
            return false
        }
        Console.printLine("success")
        return true
    }

    @class
    method main(args:Array<String>) {
        if args.get_count() != 3 {
            Console.printLine("usage: testrunner <pandac path> <test dir>")
            System.exit(1)
        }
        def pandac := File(args[1])
        def tests := File(args[2])
        def files := tests.list()
        var total := 0
        var success := 0
        for file in files {
            if file.path.endsWith(".panda") {
                total += 1
                def name := file.name()[0 .. file.name().length() - ".panda".length()]
                if runTest(pandac, tests, name) {
                    success += 1
                }
            }
        }
        Console.printLine("Passed \{success} / \{total} tests.")
    }
}