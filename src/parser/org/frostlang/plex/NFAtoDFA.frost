package org.frostlang.plex

uses frost.unsafe.Pointer
uses org.frostlang.plex.AcceptState
uses org.frostlang.plex.NFA
uses org.frostlang.plex.RemappedState
uses org.frostlang.plex.runtime.DFA

class NFAtoDFA {
    var nfa:NFA
    def states := HashMap<DFAStateLabel, DFAState>()
    def transitions := Array<List<Int>>()
    def accepts := Array<Int>()

    init(nfa:NFA) {
        self.nfa := nfa
    }

    method getState(label:DFAStateLabel):DFAState {
        var result := states[label]
        if result == null {
            result := DFAState(states.count, label)
            states[label] := result
        }
        return result
    }

    method add(nfaState:Int, states:List<Int>) {
        def state := nfa.states[nfaState]
        if state.kind = State.REMAPPED {
            def next := state->RemappedState.states
            for n in next {
                add(n, states)
            }
        }
        else {
            for s in states {
                if s = nfaState {
                    return
                }
            }
            states.add(nfaState)
        }
    }

    method addTransition(c:Char8, start:Int, next:Int) {
        while transitions.count <= c.convert() {
            transitions.add(Array<Int>())
        }
        def a := transitions[c.convert()]
        while a.count <= start {
            a.add(-1)
        }
        assert a[start] = -1
        a[start] := next
    }

    method scanState(state:DFAState) {
        state.scanned := true
        for c in DFA.START_CHAR ... DFA.END_CHAR  { -- FIXME iterate over Char8 directly
            def next := Array<Int>()
            var bestAccept := Int64.MAX
            for idx in state.label.states {
                def nfaState := nfa.states[idx]
                if nfaState.accept(Char8(c)) {
                    outer: for nextState in nfaState.next {
                        if nfa.states[nextState].kind = State.ACCEPT {
                            bestAccept := bestAccept.min(
                                    (nfa.states[nextState]->State->AcceptState).token) -- fixme remove redundant cast and ()
                        }
                        add(nextState, next)
                    }
                }
            }

            next.sortInPlace(Int.>)
            def nextState := getState(DFAStateLabel(next))
            addTransition(Char8(c), state.id, nextState.id)
            if bestAccept < Int32.MAX.convert()->Int64 {
                while accepts.count <= nextState.id {
                    accepts.add(-1)
                }
                accepts[nextState.id] := bestAccept
            }
            if !nextState.scanned {
                scanState(nextState)
            }
        }
    }

    method createDFA():DFA {
        -- create state 0, the "reject" state
        getState(DFAStateLabel(Array<Int>()))
        -- map DFA states to the set of NFA states each represents
        def startStates := Array<Int>()
        for i in 0 .. nfa.startStates.count {
            startStates.add(nfa.startStates[i])
        }
        startStates.sort(Int.>)
        -- state 1 is the start state
        def start := getState(DFAStateLabel(startStates))
        scanState(start)

        var stateCount := 0
        for i in 0 .. transitions.count {
            stateCount := stateCount.max(transitions[i].count)
        }
        def outTransitions := Pointer<Pointer<Int>>.alloc(transitions.count)
        for i in 0 ... DFA.END_CHAR.convert()->Int {
            outTransitions[i] := Pointer<Int>.alloc(stateCount)
            for j in 0 .. stateCount {
                if i < transitions.count & j < transitions[i].count {
                    outTransitions[i][j] := transitions[i][j]
                }
                else {
                    outTransitions[i][j] := 0
                }
            }
        }
        def outAccepts := Pointer<Int>.alloc(accepts.count)
        for i in 0 .. stateCount {
            if i >= accepts.count {
                outAccepts[i] := -1
            }
            else {
                outAccepts[i] := accepts[i]
            }
        }
        return DFA(stateCount, outTransitions, outAccepts)
    }
}