package org.pandalanguage.plex

class DFAStateLabel (Key<DFAStateLabel>) {
    def states:List<Int>

    init(states:List<Int>) {
        self.states := states
    }

    @override
    function hash():Int {
        var result := 0
        for s in states {
            result ~~= s
        }
        return result
    }

    @override
    function =(other:DFAStateLabel):Bit {
        if other.states.get_count() != states.get_count() {
            return false
        }
        for i in 0 .. states.get_count() {
            if other.states[i] != states[i] {
                return false
            }
        }
        return true
    }

    @override
    function !=(other:DFAStateLabel):Bit {
        return !(self = other)
    }

    @override
    function convert():String {
        def result := MutableString("<")
        var separator := ""
        for s in states {
            result.append(separator)
            result.append(s)
            separator := ", "
        }
        result.append(">")
        return result.convert()
    }
}
