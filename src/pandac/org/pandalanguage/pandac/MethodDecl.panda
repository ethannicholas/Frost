package org.pandalanguage.pandac

class MethodDecl : Symbol {
    class Parameter {
        def name:String

        def type:org.pandalanguage.pandac.Type -- FIXME shouldn't have to fully qualify this

        init(name:String, type:org.pandalanguage.pandac.Type) {
            self.name := name
            self.type := type
        }

        @override
        function convert():String {
            return "\{name}:\{type}"
        }
    }

    class Kind {
        constant METHOD   := 57
        constant FUNCTION := 58
        constant INIT     := 59
    }

    def owner:ClassDecl -- FIXME weak

    def doccomment:String?

    def annotations:Annotations

    def methodKind:Int

    def parameters:Array<Parameter>

    def returnType:Type

    def body:ASTNode?

    var resolved:Bit

    init(owner:ClassDecl, offset:Int, doccomment:String?, annotations:Annotations, methodKind:Int,
            name:String, parameters:Array<Parameter>, returnType:Type, body:ASTNode?) {
        super.init(Symbol.Kind.METHOD, offset, name)
        self.owner := owner
        self.doccomment := doccomment
        self.annotations := annotations
        self.methodKind := methodKind
        self.parameters := parameters
        self.returnType := returnType
        self.body := body
        self.resolved := false
    }

    function signature():String {
        def result := MutableString("method \{name}(")
        var separator := ""
        for p in parameters {
            result.append(separator)
            result.append(p)
            separator := ", "
        }
        result.append(")")
        if returnType != Type.Void() {
            result.append(":\{returnType}")
        }
        return result.convert()
    }

    @override
    function convert():String {
        if body != null {
            return "\{signature()} \{body}"
        }
        return signature()
    }
}