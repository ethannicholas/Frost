package org.pandalanguage.pandac.expression

uses org.pandalanguage.pandac.ASTNode
uses org.pandalanguage.pandac.Compiler
uses org.pandalanguage.pandac.Compiler.TypeContext
uses org.pandalanguage.pandac.IR
uses org.pandalanguage.pandac.Position
uses org.pandalanguage.pandac.Type

class Identifier {
    @class
    method compile(compiler:Compiler, position:Position, name:String):IR.Value? {
        if name = Compiler.FORCE_SELF_NAME {
            def s := compiler.symbolTable[Compiler.SELF_NAME]
            assert s !== null, "no 'self' in symboltable"
            return compiler.symbolRef(position, null, s)
        }
        def s := compiler.symbolTable[name]
        if s == null {
            def cl := compiler.tryResolveClass(name)
            if cl !== null {
                compiler.checkGenericArguments(position, cl, cl.type)
                return IR.Value.TYPE(cl.type)
            }
        }
        if s !== null {
            return compiler.symbolRef(position, null, s)
        }
        compiler.error(position, "unknown identifier")
        return null
    }
}