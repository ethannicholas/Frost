package org.pandalanguage.pandac.statement

uses org.pandalanguage.pandac.ASTNode
uses org.pandalanguage.pandac.Compiler
uses org.pandalanguage.pandac.Compiler.AutoContext
uses org.pandalanguage.pandac.Compiler.AutoScope
uses org.pandalanguage.pandac.Compiler.AutoUnrefs
uses org.pandalanguage.pandac.Compiler.EnclosingContext
uses org.pandalanguage.pandac.Compiler.FieldLValue
uses org.pandalanguage.pandac.Compiler.TypeContext
uses org.pandalanguage.pandac.FixedArray
uses org.pandalanguage.pandac.IR
uses org.pandalanguage.pandac.FieldDecl
uses org.pandalanguage.pandac.MethodDecl
uses org.pandalanguage.pandac.Position
uses org.pandalanguage.pandac.Type
uses org.pandalanguage.pandac.lvalue.FieldLValue

class Return {
    @class
    @pre(compiler.returnValue == null)
    method compile(compiler:Compiler, position:Position, value:ASTNode?) {
        def ir := compiler.ir
        def currentClass := compiler.currentClass[0]
        def currentMethod := compiler.currentMethod[0]
        if currentMethod.name = "cleanup" & currentMethod.parameters.count = 0 &
                currentMethod.owner.name != Compiler.WEAK_NAME {
            -- call super cleanup
            if currentClass.rawSuper !== null & !compiler.isValue(currentClass) {
                def supertype := compiler.remapType(currentClass.type, currentClass.rawSuper)
                compiler.compileStatement(ASTNode.CALL(position, ASTNode.DOT(position,
                        ASTNode.SUPER(position), "cleanup"), FixedArray<ASTNode>()))
            }
            -- unref fields
            assert !compiler.inFieldCleanup
            compiler.inFieldCleanup := true
            for f in currentMethod.owner.fields {
                if compiler.resolve(f) & !f.annotations.isClass &
                        f.fieldKind != FieldDecl.Kind.PROPERTY & compiler.isRefCounted(f.type) {
                    def load := FieldLValue(compiler, position,
                            compiler.compileExpression(ASTNode.IDENTIFIER(position,
                                    Compiler.FORCE_SELF_NAME), TypeContext.UNSPECIFIED),
                            f).compileLoad()
                    if load == null {
                        return
                    }
                    compiler.compileUnref(load)
                }
            }
            assert compiler.inFieldCleanup
            compiler.inFieldCleanup := false
        }
        if value !== null {
            if currentMethod.returnType = Type.Void() {
                compiler.error(position, "cannot return a value from a method with no return type")
                return
            }
            def compiled := compiler.compileExpression(value,
                    TypeContext.TYPE(currentMethod.returnType))
            if compiled == null {
                return
            }
            compiler.returnValue := compiled
            if compiler.isRefCounted(compiled.type()) {
                compiler.compileRef(compiled)
            }
            compiler.leaveAllScopes()
            compiler.compilePostconditions()
            ir.add(IR.Statement.RETURN(position, compiled))
            compiler.returnValue := null
        }
        else {
            if currentMethod.returnType != Type.Void() {
                if currentMethod.methodKind = MethodDecl.Kind.INIT {
                    compile(compiler, position, ASTNode.SELF(position))
                    return
                }
                compiler.error(position, "expected a return value of type " +
                        "'\{currentMethod.returnType}'")
            }
            compiler.leaveAllScopes()
            compiler.compilePostconditions()
            ir.add(IR.Statement.RETURN(position, null))
        }
    }
}