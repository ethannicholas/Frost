package org.pandalanguage.pandac

@final
class FieldDecl : Symbol {
    choice Kind {
        VAR
        DEF
        CONSTANT
        PROPERTY
    }

    @weak
    def owner:ClassDecl

    def doccomment:String?

    def annotations:Annotations

    def fieldKind:Kind

    def type:Type

    def rawValue:ASTNode?

    def value:IRNode?

    var resolved := false

    init(owner:ClassDecl, position:Position, doccomment:String?, annotations:Annotations,
            fieldKind:Kind, name:String, type:Type, value:ASTNode?) {
        self.owner := owner
        self.doccomment := doccomment
        self.annotations := annotations
        self.fieldKind := fieldKind
        self.type := type
        self.rawValue := value
        self.value := null
        super.init(Symbol.Kind.FIELD, position, name)
    }

    @override
    function convert():String {
        def result := MutableString(annotations.convert())
        match fieldKind {
            when Kind.VAR:      result.append("var ")
            when Kind.DEF:      result.append("def ")
            when Kind.CONSTANT: result.append("constant ")
            when Kind.PROPERTY: result.append("property ")
        }
        result.append(name)
        result.append(":")
        result.append(type)
        if value != null {
            result.append(" := ")
            result.append(value)
        }
        else if rawValue != null {
            result.append(" := ")
            result.append(rawValue)
        }
        return result.finish()
    }
}