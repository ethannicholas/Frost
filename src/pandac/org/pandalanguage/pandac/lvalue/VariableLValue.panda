package org.pandalanguage.pandac.lvalue

uses org.pandalanguage.pandac.Compiler
uses org.pandalanguage.pandac.IR
uses org.pandalanguage.pandac.Position
uses org.pandalanguage.pandac.Type
uses org.pandalanguage.pandac.Variable

class VariableLValue : LValue {
    def compiler:Compiler

    def position:Position

    def slot:Int

    init(compiler:Compiler, position:Position, variable:Variable) {
        self.compiler := compiler
        match variable.storage {
            when Variable.Storage.LOCAL(slot) {
                self.slot := slot
            }
            otherwise {
                assert false
            }
        }
        self.position := position
    }

    @override
    function type():Type {
        return compiler.ir.locals[slot]
    }

    @override
    method compileLoad():IR.Value? {
        def result := compiler.ir.add(IR.Statement.LOAD(position, IR.Value.LOCAL(slot, type())))
        return IR.Value.REF(result, compiler.ir.locals[slot])
    }

    @override
    method compileStore(value:IR.Value) {
        if compiler.isRefCounted(type()) {
            compiler.compileRef(value)
            compiler.compileUnref(compileLoad())
        }
        compiler.ir.add(IR.Statement.STORE(position, value, IR.Value.LOCAL(slot, type())))
    }
}