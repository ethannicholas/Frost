package org.pandalanguage.pandac

@final
class MethodRef {
    def value:MethodDecl

    def types:ListView<Type>

    def typeMap := HashMap<String, Type>()

    init(value:MethodDecl, types:ListView<Type>) {
        assert value.resolved
        self.value := value
        self.types := types
        for i in 0 .. value.owner.parameters.get_count().min(types.get_count()) {
            typeMap[value.owner.name + "." + value.owner.parameters[i].name] := types[i]
        }
    }

    function owner():Type {
        return value.owner.typeWithParameters().remap(typeMap)
    }

    function parameterCount():Int {
        return value.parameters.get_count()
    }

    function parameter(index:Int):Type {
        return value.parameters[index].type.remap(typeMap)
    }

    function returnType():Type {
        return value.returnType.remap(typeMap)
    }

    function type():Type {
        return Type("<method>", Type.Kind.INVALID, -1, true)
    }

    @override
    function convert():String {
        return "MethodRef(\{value.signature()}(\{value.owner.name}), \{types})"
    }
}