package org.pandalanguage.pandac

@final
class ClassDecl : Symbol {    
    class Kind {
        constant CLASS     := 5011
        constant INTERFACE := 5012
    }

    class GenericParameter : Symbol {
        def owner:String

        def bound:org.pandalanguage.pandac.Type -- FIXME unnecessary qualification

        init(position:Position, owner:String, name:String, bound:org.pandalanguage.pandac.Type) { -- FIXME qualification
            self.owner := owner
            self.bound := bound
            super.init(Symbol.Kind.GENERIC_PARAMETER, position, name)
        }

        @override
        function convert():String {
            return "\{owner}.\{name}:\{bound}"
        }
    }

    class Resolution : Immutable {
        def value:ClassDecl?

        init(value:ClassDecl?) {
            self.value := value
        }
    }

    def source:File

    ================================================================================================
    Maps simple class names to fully-qualified class names. This is populated from "uses" statements
    which were in effect when this class was parsed as well as built-in implicit aliases.
    ================================================================================================
    def aliases:MapView<String, String>

    def doccomment:String?

    def annotations:Annotations

    def classKind:Int

    ================================================================================================
    All declared superclasses / superinterfaces as listed in the source code. These are always
    unresolved; the resolved supertypes are used to populate rawSuper and rawInterfaces.
    ================================================================================================
    def declaredSupers:Array<Type>

    ================================================================================================
    "Raw" in the sense that it has not yet been remapped. For example, the rawSuper of HashMap is
    Map<HashMap.K, HashMap.V>, which is then remapped to figure out the supertype of a particular
    instantiation of HashMap.

    This remains null until the class is resolved.
    ================================================================================================
    def rawSuper:Type? := null

    ================================================================================================
    Comments from rawSuper apply.
    ================================================================================================
    def rawInterfaces := Array<Type>()

    def parameters:List<GenericParameter>

    def fields := Array<FieldDecl>()

    def methods := Array<MethodDecl>()

    def classes := Array<ClassDecl>()

    def owner:ClassDecl? := null

    def symbolTable:SymbolTable

    -- true if we have added all of the parent symboltables to this one
    var symbolTableResolved := false

    var external := true

    def virtualMethods := Array<MethodDecl>()

    def classResolutionCache := HashMap<String, Resolution>()

    var resolved:Bit

    init(source:File, position:Position, aliases:MapView<String, String>, doccomment:String?,
            annotations:Annotations, kind:Int, name:String, declaredSupers:ListView<Type>,
            parameters:Array<GenericParameter>, parent:SymbolTable) {
        super.init(Symbol.Kind.CLASS, position, name)
        self.source := source
        self.aliases := HashMap<String, String>(aliases)
        self.doccomment := doccomment
        self.annotations := annotations
        self.classKind := kind
        self.declaredSupers := Array<Type>(declaredSupers)
        self.parameters := parameters
        self.symbolTable := SymbolTable(parent)
        self.resolved := false
        for p in parameters {
            symbolTable.add(p)
        }
    }

    function type():Type {
        return Type(name, Type.Kind.CLASS, Position(), true)
    }

    function typeWithParameters():Type {
        if parameters.get_count() = 0 {
            return type()
        }
        def subtypes := Array<Type>()
        subtypes.add(type())
        def name := MutableString(self.name)
        name.append("<")
        var separator := ""
        for p in parameters {
            def t := Type(self.name + "." + p.name, Type.Kind.GENERIC_PARAMETER, Position(), true)
            subtypes.add(t)
            name.append(separator)
            name.append(t.name)
            separator := ", "
        }
        name.append(">")
        return Type(name.finish(), Type.Kind.GENERIC, Position(), subtypes, true)
    }

    function isSpecialization():Bit {
        return name.contains("<")
    }

    @override
    function convert():String {
        def result := MutableString("class \{name}")
        if rawSuper != Type.Void() {
            result.append(":\{rawSuper}")
        }
        if rawInterfaces.get_count() > 0 {
            result.append(" (")
            var separator := ""
            for intf in rawInterfaces {
                result.append("\{separator}\{intf}")
                separator := ", "
            }
            result.append(")")
        }
        result.append(" {\n")
        for f in fields {
            result.append("\{f}\n")
        }
        for m in methods {
            result.append("\{m}\n")
        }
        result.append("}")
        return result.finish()
    }
}