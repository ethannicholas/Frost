package org.frostlanguage.frostc.statement

uses org.frostlanguage.frostc.ASTNode
uses org.frostlanguage.frostc.Compiler
uses org.frostlanguage.frostc.Compiler.AutoContext
uses org.frostlanguage.frostc.Compiler.AutoScope
uses org.frostlanguage.frostc.Compiler.AutoUnrefs
uses org.frostlanguage.frostc.Compiler.EnclosingContext
uses org.frostlanguage.frostc.Compiler.FieldLValue
uses org.frostlanguage.frostc.Compiler.TypeContext
uses org.frostlanguage.frostc.FixedArray
uses org.frostlanguage.frostc.IR
uses org.frostlanguage.frostc.FieldDecl
uses org.frostlanguage.frostc.MethodDecl
uses org.frostlanguage.frostc.Position
uses org.frostlanguage.frostc.Type
uses org.frostlanguage.frostc.lvalue.FieldLValue

class Return {
    @class
    @pre(compiler.returnValue == null)
    method compile(compiler:Compiler, position:Position, value:ASTNode?) {
        def ir := compiler.ir
        def currentClass := compiler.currentClass[0]
        def currentMethod := compiler.currentMethod[0]
        if currentMethod.name = "cleanup" & currentMethod.parameters.count = 0 &
                currentMethod.owner.name != Compiler.WEAK_NAME {
            -- call super cleanup
            if currentClass.rawSuper !== null & !compiler.isValue(currentClass) {
                def supertype := compiler.remapType(currentClass.type, currentClass.rawSuper)
                compiler.compileStatement(ASTNode.CALL(position, ASTNode.DOT(position,
                        ASTNode.SUPER(position), "cleanup"), FixedArray<ASTNode>()))
            }
            -- unref fields
            assert !compiler.inFieldCleanup
            compiler.inFieldCleanup := true
            for f in currentMethod.owner.fields {
                if compiler.resolve(f) & !f.annotations.isClass &
                        f.fieldKind != FieldDecl.Kind.PROPERTY & compiler.isRefCounted(f.type) {
                    def load := FieldLValue(compiler, position,
                            compiler.compileExpression(ASTNode.IDENTIFIER(position,
                                    Compiler.FORCE_SELF_NAME), TypeContext.UNSPECIFIED),
                            f).compileLoad()
                    if load == null {
                        return
                    }
                    compiler.compileUnref(load)
                }
            }
            assert compiler.inFieldCleanup
            compiler.inFieldCleanup := false
        }
        if value !== null {
            if currentMethod.returnType = Type.Void() {
                compiler.error(position, "cannot return a value from a method with no return type")
                return
            }
            def compiled := compiler.compileExpression(value,
                    TypeContext.TYPE(currentMethod.returnType))
            if compiled == null {
                return
            }
            compiler.returnValue := compiled
            if compiler.isRefCounted(compiled.type()) {
                compiler.compileRef(compiled)
            }
            compiler.leaveAllScopes()
            compiler.compilePostconditions()
            ir.add(IR.Statement.RETURN(position, compiled))
            compiler.returnValue := null
        }
        else {
            if currentMethod.returnType != Type.Void() {
                if currentMethod.methodKind = MethodDecl.Kind.INIT {
                    compile(compiler, position, ASTNode.SELF(position))
                    return
                }
                compiler.error(position, "expected a return value of type " +
                        "'\{currentMethod.returnType}'")
            }
            compiler.leaveAllScopes()
            compiler.compilePostconditions()
            ir.add(IR.Statement.RETURN(position, null))
        }
    }
}