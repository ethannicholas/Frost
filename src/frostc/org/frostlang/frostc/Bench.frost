package org.frostlang.frostc

-*

Keep in mind that the compile times include only the Frost side of things, so we're not counting
Clang's compilation time here.

10/9/19
Compile-Hello-O0: 0.143355
Compile-Hello-O3: 0.147332
Compile-Wumpus-O0: 1.07063
Compile-Wumpus-O3: 1.37096
Compile-Big-O0: 0.312204
Compile-Big-O3: 1.51414
Fasta: 2.34387

11/12/19 (stubs, -S0)
Compile-Hello-O0: 0.0459104
Compile-Hello-O3: 0.0392957
Compile-Wumpus-O0: 0.253385
Compile-Wumpus-O3: 0.274485
Compile-Big-O0: 0.145444
Compile-Big-O3: 0.142478
Fasta: 1.68861
Fasta is slower than it should be because I had to disable a RefUnrefPair optimization. Should be
able to reenable when function safety is fixed.

*-

class Bench {
    constant COUNT := 3

    method benchCompile(name:String, src:String, optimizationLevel:Int, safetyLevel:Int) {
        def timer := frost.time.Timer()
        timer.reset()
        for i in 0 .. COUNT {
            def settings := Compiler.Settings(File(Config.frostSourceDir()),
                    [File(Config.frostLibDir()).resolve("stubs"), File(Config.frostSourceDir())],
                    optimizationLevel, safetyLevel, 8, false, false)
            def errorQueue := MessageQueue<Compiler.Message>()
            def out := MemoryOutputStream()
            def backend := LLVMBackend(Config.llvmTriple(), out)
            def compiler := Compiler(errorQueue, backend, settings)
            compiler.compile(File("/fake/path/BenchDummy.frost"), src)
            compiler.finish()
            while errorQueue.hasMessage() {
                def error := errorQueue.getMessage()
                match error {
                    when Compiler.Message.ERROR(file, line, column, message) {
                        Console.printLine("\{file}:\{line}:\{column}: error: \{message}")
                    }
                    when Compiler.Message.WARNING(file, line, column, message) {
                        Console.printLine("\{file}:\{line}:\{column}: warning: \{message}")
                    }
                }
            }
            assert(compiler.errorCount = 0)
        }
        Console.printLine("Compile-\{name}: \{timer.elapsed() / COUNT}")
    }

    method benchCompile(name:String, src:String) {
        benchCompile(name + "-O0", src, 0, 0)
        benchCompile(name + "-O3", src, 3, 3)
    }

    method benchRun(name:String, file:File) {
        def dst := System.temporaryDirectory().resolve("bench").path
        def args := ["-S", "0", "-O", "3", "-o", dst, file.path]
        Compiler.run(args)
        def timer := frost.time.Timer()
        timer.reset()
        for i in 0 .. COUNT {
            try {
                def p := System.exec(dst, [])
                def received := p.standardOutput().readFully()
                def expected := file.withExtension(".expected").readFully()
                if received != expected {
                    Console.printLine("\{name} expected:\n\{expected}\nbut received:\n\{received}")
                    System.exit(1)
                }
                p.waitFor()
            }
            fail(error) {
                Console.printLine(error)
                System.exit(1)
            }
        }
        Console.printLine("\{name}: \{timer.elapsed() / COUNT}")
    }

    method run() {
        try {
            Console.printLine("Times in seconds, smaller is better")
            benchCompile("Hello", "method main() { Console.printLine(\"Hello, World!\") }")
            def wumpus := File(Config.frostSourceDir()).resolve("../test/tests/Wumpus.frost")
            benchCompile("Wumpus", wumpus.readFully())
            def big := MutableString("method main() {\n")
            for i in 1 ... 1000 {
                big.append("    Console.printLine(\"\{i}\")")
            }
            big.append("}")
            benchCompile("Big", big.finish())
            benchRun("Fasta", File(Config.frostSourceDir()).resolve("../benchmarks/Fasta.frost"))
        }
        fail(err) {
            Console.printLine(err)
            System.crash()
        }
    }
}