package org.frostlang.frostc.expression

uses org.frostlang.frostc.ASTNode
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.Compiler.TypeContext
uses org.frostlang.frostc.FixedArray
uses org.frostlang.frostc.IR
uses org.frostlang.frostc.MethodPosition
uses org.frostlang.frostc.Type

class ArrayLiteral {
    @class
    method compile(compiler:Compiler, position:MethodPosition, elements:FixedArray<ASTNode>,
            type:TypeContext):IR.Value?  {
        var listType:(Bit, Type)?
        match type {
            when TypeContext.TYPE(t, _) {
                listType := compiler.getListType(t)
                if listType == null {
                    -- it's going to cause an error no matter what we do, so let's just go with the
                    -- preferred type for error reporting purposes
                    def preferred := compiler.preferredType(ASTNode.ARRAY(position.asPosition,
                            elements))
                    if preferred !== null {
                        listType := compiler.getListType(preferred)
                    }
                    if listType == null {
                        compiler.error(position, "cannot determine type of array literal")
                        return null
                    }
                }
            }
            otherwise {
                def t := compiler.preferredType(ASTNode.ARRAY(position.asPosition, elements))
                if t == null {
                    compiler.error(position, "cannot determine type of array literal")
                    return null
                }
                listType := compiler.getListType(t)
            }
        }
        def arrayType := Type.ArrayOf(compiler, listType[1])
        var result := Call.compile(compiler, position, compiler.literalType(arrayType),
                [ASTNode.INT(position.asPosition, elements.count.asUInt64)],
                TypeContext.UNSPECIFIED)
        if result == null {
            return null
        }
        for e in elements {
            Call.compile(compiler, position, result, "add", [e], TypeContext.UNSPECIFIED)
        }
        if !listType[0] {
            result := Call.compile(compiler, position,
                    compiler.literalType(Type.ImmutableArrayOf(compiler, listType[1])), "from",
                    [ASTNode.IR_WRAPPER(position.asPosition, result)], TypeContext.UNSPECIFIED)
            if result == null {
                return null
            }
        }
        return compiler.coerce(position, result, type)
    }
}