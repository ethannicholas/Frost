package org.frostlang.frostc.expression

uses org.frostlang.frostc.ASTNode
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.Compiler.AutoUnrefs
uses org.frostlang.frostc.Compiler.TypeContext
uses org.frostlang.frostc.IR
uses org.frostlang.frostc.MethodPosition
uses org.frostlang.frostc.Type
uses org.frostlang.frostc.Variable

class Or {
    @class
    method compile(compiler:Compiler, position:MethodPosition, left:ASTNode, right:ASTNode,
            type:TypeContext):IR.Value? {
        def ir := compiler.ir
        -- FIXME should add phis so we can avoid using a local        
        def result := IR.Value.LOCAL(ir.locals.count, compiler.BIT_TYPE)
        ir.add(IR.Statement.ENTER_SCOPE(ir.locals.count))
        ir.locals.add(("<internal>", Variable.Kind.DEF, compiler.BIT_TYPE))
        def compiledLeft:IR.Value?
        compiledLeft := compiler.compileExpression(left, TypeContext.TYPE(compiler.BIT_TYPE, false))
        if compiledLeft == null {
            return null
        }
        def ifTrue := ir.newBlock("or true")
        def ifFalse := ir.newBlock("or false")
        def merge := ir.newBlock("or merge")
        ir.add(IR.Statement.CONDITIONAL_BRANCH(position,
                compiler.extractBuiltinBit(position, compiledLeft), ifTrue, ifFalse))
        ir.setCurrentBlock(ifTrue)
        ir.add(IR.Statement.STORE(position, compiledLeft, result, true))
        ir.add(IR.Statement.BRANCH(position, merge))
        ir.setCurrentBlock(ifFalse)
        def compiledRight:IR.Value?
        {
            def unrefs := AutoUnrefs(compiler)
            compiledRight := compiler.compileExpression(right, TypeContext.TYPE(compiler.BIT_TYPE,
                    false))
            if compiledRight == null {
                return null
            }
        }
        ir.add(IR.Statement.STORE(position, compiledRight, result, true))
        ir.add(IR.Statement.BRANCH(position, merge))
        ir.setCurrentBlock(merge)
        def load := ir.add(IR.Statement.LOAD(position, result))
        return IR.Value.REF(load, compiler.BIT_TYPE)
    }

    @class
    method compileConditionalBranch(compiler:Compiler, left:ASTNode, right:ASTNode,
            ifTrue:IR.Block.ID, ifFalse:IR.Block.ID) {
        def leftFalse := compiler.ir.newBlock("or left false")
        compiler.compileConditionalBranch(left, ifTrue, leftFalse)
        compiler.ir.setCurrentBlock(leftFalse)
        compiler.compileConditionalBranch(right, ifTrue, ifFalse)
    }
}