package org.frostlang.frostc

@final
class Position : Value, HashKey<Position>, Comparable<Position> {
    -- index into the Compiler.file array
    def file:Int

    def line:Int

    def column:Int

    init() {
        self.file := -1
        self.line := 1
        self.column := 1
    }

    init(file:Int, line:Int, column:Int) {
        assert line != 0 & column != 0
        self.file := file
        self.line := line
        self.column := column
    }

    @override
    function =(other:Position):Bit {
        return file = other.file & line = other.line & column = other.column
    }

    @override
    function >(other:Position):Bit {
        if line > other.line {
            return true
        }
        if line < other.line {
            return false
        }
        return column > other.column
    }

    @override
    function get_hash():Int {
        return file << 24 + line << 16 + column
    }

    @override
    function get_toString():String {
        return "\{line}:\{column}"
    }

    function toString(compiler:Compiler):String {
        if file = -1 {
            return "<internal>:\{line}:\{column}"
        }
        return "\{compiler.files[file].name}:\{line}:\{column}"
    }
}

