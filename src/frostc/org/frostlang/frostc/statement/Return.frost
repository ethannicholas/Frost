package org.frostlang.frostc.statement

uses org.frostlang.frostc.ASTNode
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.Compiler.AutoContext
uses org.frostlang.frostc.Compiler.AutoScope
uses org.frostlang.frostc.Compiler.AutoUnrefs
uses org.frostlang.frostc.Compiler.EnclosingContext
uses org.frostlang.frostc.Compiler.FieldLValue
uses org.frostlang.frostc.Compiler.TypeContext
uses org.frostlang.frostc.FixedArray
uses org.frostlang.frostc.IR
uses org.frostlang.frostc.FieldDecl
uses org.frostlang.frostc.MethodDecl
uses org.frostlang.frostc.MethodPosition
uses org.frostlang.frostc.Type
uses org.frostlang.frostc.Variable
uses org.frostlang.frostc.lvalue.FieldLValue
uses org.frostlang.frostc.lvalue.VariableLValue

class Return {
    @class
    method compile(compiler:Compiler, position:MethodPosition, value:ASTNode?) {
        compile(compiler, position, value, true)
    }

    @class
    method compile(compiler:Compiler, position:MethodPosition, value:ASTNode?, refResult:Bit) {
        def ir := compiler.ir
        def currentClass := compiler.currentClass[0]
        def currentMethod := compiler.currentMethod[0]
        def basePos := position.asPosition
        if currentMethod.name = "cleanup" & currentMethod.parameters.count = 0 &
                currentMethod.owner.name != Compiler.WEAK_NAME {
            -- call super cleanup
            if currentClass.rawSuper !== null & !compiler.isValue(currentClass) {
                def supertype := compiler.remapType(currentClass.type, currentClass.rawSuper)
                compiler.compileStatement(ASTNode.CALL(basePos, ASTNode.DOT(basePos,
                        ASTNode.SUPER(basePos), "cleanup"), FixedArray<ASTNode>()))
            }
            -- unref fields
            assert !compiler.inFieldCleanup
            compiler.inFieldCleanup := true
            for f in currentMethod.owner.fields {
                if compiler.resolve(f) & !f.annotations.isClass &
                        f.fieldKind != FieldDecl.Kind.PROPERTY & compiler.isRefCounted(f.type) {
                    def load := FieldLValue(compiler, position,
                            compiler.compileExpression(ASTNode.IDENTIFIER(basePos,
                                    Compiler.FORCE_SELF_NAME), TypeContext.NON_VOID),
                            f).compileLoad()
                    if load == null {
                        return
                    }
                    compiler.compileUnref(load)
                }
            }
            assert compiler.inFieldCleanup
            compiler.inFieldCleanup := false
        }
        if value !== null {
            if currentMethod.returnType = compiler.VOID_TYPE {
                compiler.error(position, "cannot return a value from a method with no return type")
                return
            }
            def compiled := compiler.compileExpression(value,
                    TypeContext.TYPE(currentMethod.returnType, false))
            if compiled == null {
                return
            }
            if refResult & compiler.isRefCounted(compiled.type) {
                compiler.compileRef(compiled)
            }
            compiler.leaveAllScopes()
            loop {
                match compiler.enclosingContexts[0] {
                    when Compiler.EnclosingContext.VALUE_SCOPE {
                        compiler.enclosingContexts.pop()
                    }
                    otherwise {
                        break
                    }
                }
            }
            if compiler.returnVar !== null {
                def returnSlot:Int
                match compiler.returnVar.storage {
                    when Variable.Storage.LOCAL(slot) {
                        returnSlot := slot
                    }
                    otherwise {
                        unreachable
                    }
                }
                ir.add(IR.Statement.STORE(position, compiled, IR.Value.LOCAL(returnSlot,
                        compiled.type), true))
                ir.add(IR.Statement.BRANCH(position, compiler.postconditionEntryBlock))
            }
            else {
                def inlineScope := compiler.getInlineContext()
                if inlineScope !== null {
                    match inlineScope {
                        when Compiler.EnclosingContext.INLINE_CONTEXT(_, _, inlineReturn,
                                mergeBlock) {
                            if inlineReturn.variable !== null {
                                def returnSlot:Int
                                match inlineReturn.variable.storage {
                                    when Variable.Storage.LOCAL(slot) {
                                        returnSlot := slot
                                    }
                                    otherwise {
                                        unreachable
                                    }
                                }
                                ir.add(IR.Statement.STORE(position, compiled,
                                        IR.Value.LOCAL(returnSlot, compiled.type), true))
                            }
                            else {
                                inlineReturn.value := compiled
                            }
                            ir.add(IR.Statement.BRANCH(position, mergeBlock))
                        }
                        otherwise {
                            unreachable
                        }
                    }
                }
                else {
                    ir.add(IR.Statement.RETURN(position, compiled))
                }
            }
        }
        else {
            if currentMethod.returnType != compiler.VOID_TYPE {
                if currentMethod.methodKind = MethodDecl.Kind.INIT {
                    compile(compiler, position, ASTNode.SELF(basePos))
                    return
                }
                compiler.error(position, "expected a return value of type " +
                        "'\{currentMethod.returnType}'")
            }
            compiler.leaveAllScopes()
            if compiler.postconditionEntryBlock !== null {
                ir.add(IR.Statement.BRANCH(position, compiler.postconditionEntryBlock))
            }
            else {
                def inlineScope := compiler.getInlineContext()
                if inlineScope !== null {
                    match inlineScope {
                        when Compiler.EnclosingContext.INLINE_CONTEXT(_, _, inlineReturn,
                                mergeBlock) {
                            if mergeBlock !== null {
                                ir.add(IR.Statement.BRANCH(position, mergeBlock))                    
                            }
                        }
                    }
                }
                else {
                    ir.add(IR.Statement.RETURN(position, null))
                }
            }
        }
    }
}