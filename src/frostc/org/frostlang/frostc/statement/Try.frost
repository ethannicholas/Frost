package org.frostlang.frostc.statement

uses org.frostlang.frostc.ASTNode
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.Compiler.AutoContext
uses org.frostlang.frostc.Compiler.AutoScope
uses org.frostlang.frostc.Compiler.EnclosingContext
uses org.frostlang.frostc.FixedArray
uses org.frostlang.frostc.IR
uses org.frostlang.frostc.MethodPosition
uses org.frostlang.frostc.Type
uses org.frostlang.frostc.Variable

class Try {
    @class
    method compile(compiler:Compiler, position:MethodPosition, block:FixedArray<ASTNode>,
            error:ASTNode?, failBlock:FixedArray<ASTNode>) {
        def scope := AutoScope(compiler)
        def ir := compiler.ir
        def failTarget := ir.newBlock("try fail")
        def endBlock := ir.newBlock("try end")
        def errorVar:Variable?
        if error !== null {
            match error {
                when ASTNode.IDENTIFIER(_, name) {
                    errorVar := Variable(position.asPosition, Variable.Kind.DEF, name,
                            compiler.ERROR_TYPE, Variable.Storage.LOCAL(ir.locals.count))
                    errorVar.owner := compiler.currentMethod[0]
                    if compiler.isRefCounted(errorVar.type) {
                        ir.add(IR.Statement.STORE(position, IR.Value.NULL(errorVar.type.nullable()),
                                IR.Value.LOCAL(ir.locals.count, errorVar.type), false))
                        compiler.enclosingContexts.push(EnclosingContext.VARIABLE_SCOPE(errorVar))
                    }
                    ir.add(IR.Statement.ENTER_SCOPE(ir.locals.count))
                    ir.locals.add((errorVar.name, errorVar.varKind, errorVar.type))
                }
                otherwise {
                    unreachable
                }
            }
        }
        else {
            errorVar := null
        }
        {
            def context := AutoContext(compiler,
                    Compiler.EnclosingContext.TRY_SCOPE(failTarget, errorVar))
            def tryScope := AutoScope(compiler)
            for s in block {
                compiler.compileStatement(s)
            }
        }
        if !ir.currentBlockFinished {
            ir.add(IR.Statement.BRANCH(position, endBlock))
        }
        ir.setCurrentBlock(failTarget)
        {
            {
                def failScope := AutoScope(compiler)
                if errorVar !== null {
                    compiler.symbolTable.add(errorVar)
                }
                for s in failBlock {
                    compiler.compileStatement(s)
                }
            }
            if !ir.currentBlockFinished {
                ir.add(IR.Statement.BRANCH(position, endBlock))
            }
        }
        ir.setCurrentBlock(endBlock)
    }
}
