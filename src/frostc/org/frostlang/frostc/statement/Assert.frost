package org.frostlang.frostc.statement

uses org.frostlang.frostc.ASTNode
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.Compiler.AutoUnrefs
uses org.frostlang.frostc.Compiler.EnclosingContext
uses org.frostlang.frostc.Compiler.TypeContext
uses org.frostlang.frostc.IR
uses org.frostlang.frostc.MethodDecl
uses org.frostlang.frostc.MethodRef
uses org.frostlang.frostc.Methods
uses org.frostlang.frostc.Position
uses org.frostlang.frostc.Type
uses org.frostlang.frostc.expression.Call

class Assert {
    @class
    method compileFail(compiler:Compiler, position:Position, msg:ASTNode?) {
        {
            def unrefs := AutoUnrefs(compiler)
            def args := Array<ASTNode>()
            args.add(ASTNode.STRING(position, compiler.currentClass[0].source.name))
            args.add(ASTNode.INT(position, position.line.convert()))
            def frost := compiler.getClass(Type.Frost())
            def m:MethodDecl
            if msg !== null {
                m := compiler.getSymbolTable(frost)["assertionFailure"]->Methods.methods[0]
                args.add(msg)
            }
            else {
                m := compiler.getSymbolTable(frost)["assertionFailure"]->Methods.methods[1]
            }
            if !compiler.resolve(m) {
                return
            }
            Call.compile(compiler, position, null, MethodRef(compiler, Type.Frost(), m), args,
                    TypeContext.UNSPECIFIED)
        }
        compiler.ir.add(IR.Statement.UNREACHABLE(position))
    }

    @class
    method compile(compiler:Compiler, position:Position, test:ASTNode, msg:ASTNode?) {
        def ir := compiler.ir
        def compiledTest := compiler.compileExpression(test, TypeContext.TYPE(Type.Bit()))
        if compiledTest == null {
            return
        }
        def success := ir.newBlock("assert success")
        def failure := ir.newBlock("assert failure")
        ir.add(IR.Statement.CONDITIONAL_BRANCH(position, compiler.extractBuiltinBit(position,
                compiledTest), success, failure))
        ir.setCurrentBlock(failure)
        compileFail(compiler, position, msg)
        ir.setCurrentBlock(success)
    }
}