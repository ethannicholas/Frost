package org.frostlang.frostc.frostdoc

uses org.frostlang.frostc.ClassDecl
uses org.frostlang.frostc.Compiler
uses org.frostlang.frostc.FieldDecl
uses org.frostlang.frostc.MethodDecl
uses org.frostlang.frostc.Methods
uses org.frostlang.frostc.Symbol
uses org.frostlang.frostc.Type
uses org.frostlang.frostc.parser.Token

class LinkResolver {
    @private
    def compiler:Compiler

    @private
    def docPath:File

    choice Context {
        CLASS(ClassDecl, String? -* api path override *-)
        PACKAGE(String)
        API_PATH(String -* api path *-, File -* from file *-)

        @override
        function get_asString():String {
            match self {
                when CLASS(cl, _) {
                    return cl.name
                }
                when PACKAGE(name) {
                    return name
                }
                when API_PATH(_, path) {
                    return path.asString
                }
                otherwise {
                    unreachable
                }
            }
        }
    }

    @private
    def context:Context?

    init(compiler:Compiler, docPath:File, context:Context) {
        self.compiler := compiler
        self.docPath := docPath
        self.context := context
    }

    method resolveInContext(m:MethodDecl, type:Type):Type {
        compiler.currentClass.push(m.owner)
        def auto := Compiler.AutoScope(compiler, compiler.getSymbolTable(m.owner))
        if m.genericParameters !== null {
            for g in m.genericParameters {
                compiler.symbolTable.add(g)
            }
        }
        def resolved := compiler.resolve(type, false)
        compiler.currentClass.pop()
        if resolved == null {
            return type
        }
        return resolved
    }

    method matches(m:MethodDecl, parameters:ListView<Type>):Bit {
        Console.printLine("matched \{m.declaration}, \{parameters}")
        if m.parameters.count != parameters.count {
            return false
        }
        compiler.resolve(m)
        for i in 0 .. parameters.count {
            if m.parameters[i].type != resolveInContext(m, parameters[i]) {
                return false
            }
        }
        return true
    }

    function getAPIPath():String {
        def base:String
        match context {
            when Context.CLASS(cl, path) {
                if path !== null {
                    return path
                }
                return "../" * cl.name.find(".").count()
            }
            when Context.PACKAGE(name) {
                return "../" * name.find(/\w+/).count()
            }
            when Context.API_PATH(path, _) {
                return path
            }
            otherwise {
                unreachable
            }
        }
    }

    function linkTo(cl:ClassDecl):String {
        return getAPIPath() + cl.name.replace(".", "/") + ".html"
    }

    function linkTo(f:FieldDecl):String? {
        return linkTo(f.owner) + "#\{f.name}"
    }

    function linkTo(m:MethodDecl):String? {
        compiler.resolve(m)
        def result := MutableString(linkTo(m.owner))
        result.append("#")
        result.append(m.name)
        result.append("(")
        var separator := ""
        for p in m.parameters {
            result.append(separator)
            result.append(p.type)
            separator := ","
        }
        result.append(")")
        if m.returnType.name != "void" {
            result.append(":")
            result.append(m.returnType)
        }
        result.replace("&", "&amp;")
        result.replace("<", "&lt;")
        return result.finish()
    }

    function linkTo(s:Symbol):String? {
        match s.kind {
            when Symbol.Kind.FIELD {
                return linkTo(s->FieldDecl)
            }
            when Symbol.Kind.METHOD {
                return linkTo(s->MethodDecl)
            }
            otherwise {
                return null
            }
        }
    }

    method resolve(link:String):String? {
        def hash := link.indexOf("#")
        if hash !== null {
            def result := resolve(link[..hash])
            if result == null {
                return null
            }
            return result + link[hash..]
        }
        if link.startsWith("http") | link.endsWith(".html") | link.startsWith("/") {
            return link
        }
        if link.startsWith("example:") {
            return getAPIPath() + "../try.html?\{link['example:'.length..]}"
        }
        if link.endsWith(".md") {
            if docPath.resolve(link).exists() {
                return link.replace(".md", ".html")
            }
            Console.printLine("link to nonexistent file \{link} in \{context}")
            return null
        }
        var cl := compiler.tryResolveClass(link)
        if cl !== null {
            return linkTo(cl)
        }
        match context {
            when Context.CLASS(cl, _) {
                if link.matches(/\w+/) {
                    def s := compiler.getSymbolTable(cl)[link]
                    if s !== null {
                        return linkTo(s)
                    }
                }
            }
        }
        -- dotted name
        if link.matches(/\w+(?:\.\w+)*/) {
            def index := link.lastIndexOf(".")
            if index !== null {
                cl := compiler.tryResolveClass(link[..index])
                if cl !== null {
                    def s := compiler.getSymbolTable(cl)[link[link.next(index)..]]
                    if s == null {
                        Console.printLine("could not resolve: \{link} in \{context}")
                        return null
                    }
                    return linkTo(s)
                }
            }
        }
        -- method signature
        def parsed := link.parse(/([^(]+)\((.*)\)(?::(.*))?/)
        if parsed !== null {
            def parameters := Array<Type>()
            if parsed[1].trimmed != "" {
                def src := File(parsed[1])
                compiler.parser.start(src, compiler.files.count, parsed[1])
                compiler.files.add(src)
                loop {
                    def type := compiler.parser.type()
                    if type == null {
                        Console.printLine("could not parse: '\{parsed[1]}'")
                        return null
                    }
                    parameters.add(compiler.scanner.convertType(type))
                    if compiler.parser.checkNext(Token.Kind.COMMA) == null {
                        break
                    }
                }
            }
            def returnType:Type? := null
            if parsed[2] != "" {
                def src := File(parsed[2])
                compiler.parser.start(src, compiler.files.count, parsed[2])
                compiler.files.add(src)
                def type := compiler.parser.type()
                if type == null {
                    Console.printLine("could not parse2: '\{parsed[2]}'")
                    return null
                }
                returnType := compiler.scanner.convertType(type)
            }
            def name := parsed[0]
            def index := name.lastIndexOf(".")
            def symbolName:String
            if index !== null {
                cl := compiler.tryResolveClass(name[..index])
                symbolName := name[name.next(index)..]
            }
            else {
                match context {
                    when Context.CLASS(contextClass, _) {
                        cl := contextClass
                    }
                }
                symbolName := name
            }
            if cl !== null {
                def s := compiler.getSymbolTable(cl)[symbolName]
                if s == null {
                    Console.printLine("no symbol '\{symbolName}' in \{context}")
                    return null
                }
                match s.kind {
                    when Symbol.Kind.METHOD {
                        if matches(s->MethodDecl, parameters) {
                            return linkTo(s)
                        }
                    }
                    when Symbol.Kind.METHODS {
                        for m in s->Methods.methods {
                            if matches(m, parameters) {
                                return linkTo(m)
                            }
                        }
                    }
                }
                Console.printLine("no match for link to method '\{link}' in \{context}")
                return null
            }
        }
        Console.printLine("no match for link to '\{link}' in \{context}")
        return null
    }
}