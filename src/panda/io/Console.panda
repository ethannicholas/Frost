package panda.io

@final
class Console {
    @class
    @external
    function inputStream():InputStream

    @class
    @external
    function outputStream():OutputStream

    @class
    @external
    function errorStream():OutputStream

    @class
    @external
    method write(c:Char8)

    @class
    method print(s:String) {
        for i in 0 .. s._length {
            Console.write(s.data[i])
        }
    }

    @class
    method printLine(s:String) {
        print(s)
        printLine()
    }

    @class
    method print(o:Object) {
        print(o.convert())
    }

    @class
    method printLine(o:Object) {
        printLine(o.convert())
    }

    @class
    method printLine() {
        print("\n")
    }

    @class
    @external
    method read():Char8?

    @class
    method readLine():String? {
        def result := MutableString()
        loop {
            def c:Char8? := read()
            if c = null {
                if result.length = 0 {
                    return null
                }
                break
            }
            if c = "\n" {
                break
            }
            result.append(c)
        }
        return result.finish()
    }
}