package panda.collections

uses panda.unsafe.Pointer

@final
class ImmutableArray<T> : Immutable, ListView<T> {
    @private
    class ImmutableArrayIterator<T> : Iterator<T> {
        var array:ImmutableArray<T>

        var index := 0

        init(array:ImmutableArray<T>) {
            self.array := array
        }

        @override
        function get_done():Bit {
            return index = array.count
        }

        @override
        function next():T {
            index += 1
            return array[index - 1]
        }
    }

    @private
    def _count:Int

    @private
    def dummy:Int -- for binary compatibility with Array

    @private
    def data:Pointer<T?>

    init() {
        self._count := 0
        data := Pointer<T?>.alloc(0)
    }

    init(data:ListView<T>) {
        self._count := data.count
        self.data := Pointer<T?>.alloc(_count)
        for i in 0 .. count {
            self.data[i] := data[i]
        }
    }

    @private
    init(data:Pointer<T>, count:Int) {
        self.data := data->Pointer<T?>
        self._count := count
    }

    @override
    method cleanup() {
        for i in 0 .. count {
            data[i] := null
        }
        data.destroy()
    }

    @override
    function [](index:Int):T {
        assert index >= 0 & index < count
        return data[index]->T
    }

    @override
    function get_count():Int {
        return _count
    }

    @override
    function iterator():Iterator<T> {
        return ImmutableArrayIterator<T>(self)
    }

    @override
    function convert():String {
        def result := MutableString()
        result.append("[")
        var separator := ""
        for v in self {
            result.append(separator)
            separator := ", "
            if v !== null {
                result.append(v)
            }
            else {
                result.append("<null>")
            }
        }
        result.append("]")
        return result.finish()
    }
}
