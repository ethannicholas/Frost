package panda.core

====================================================================================================
Similar to [Range], but with a `step` value that indicates the number of steps to take during each
iteration. For instance, the code:

    for i in SteppedRange<Int, Int>(0, 100, 10, true) {
        Console.printLine(i)
    }

will count by 10 and print the numbers 0, 10, 20, ..., 90, 100. The exclusive range (`..`) and
inclusive range (`...`) operators provide a shorthand syntax for creating `Range` and `SteppedRange`
objects; the loop above could be more succinctly (and readably) written:

    for i in 0 ... 100 by 10 {
        Console.printLine(i)
    }
====================================================================================================
@final
@specialize
class SteppedRange<EndPoint:Value?, Step:Value> : Value {
    def start:EndPoint

    def end:EndPoint

    def step:Step

    def inclusive:Bit

    init(start:EndPoint, end:EndPoint, step:Step, inclusive:Bit) {
        self.start := start
        self.end := end
        self.step := step
        self.inclusive := inclusive
    }

    @override
    function convert():String {
        def result := MutableString()
        if start != null {
            result.append(start)
        }
        if inclusive {
            result.append("...")
        }
        else {
            result.append("..")
        }
        if end != null {
            result.append(end)
        }
        result.append(" by ")
        result.append(step)
        return result.finish()
    }
}