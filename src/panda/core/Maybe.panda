package panda.core

====================================================================================================
Holds either a value or an `Error`. `Maybe` is intended for use as a return type for a method which
returns a value but is not always guaranteed to succeed.
====================================================================================================
choice Maybe<T> {
    ================================================================================================
    A successful call with return value.
    ================================================================================================
    SUCCESS(T)

    ================================================================================================
    An error occurred.
    ================================================================================================
    ERROR(Error)

    ================================================================================================
    Returns the result of a successful call.
    ================================================================================================
    @pre(succeeded())
    function get():T {
        match self {
            when SUCCESS(value) {
                return value
            }
        }
        assert false
    }

    ================================================================================================
    Returns the error message of a failed call.
    ================================================================================================
    @pre(!succeeded())
    function error():Error {
        match self {
            when ERROR(error) {
                return error
            }
        }
        assert false
    }

    ================================================================================================
    Returns `true` if the call succeeded.
    ================================================================================================
    function succeeded():Bit {
        match self {
            when SUCCESS {
                return true
            }
            when ERROR {
                return false
            }
        }
    }

    @override
    function convert():String {
        match self {
            when SUCCESS(v) {
                return "SUCCESS(\{v})"
            }
            when ERROR(e) {
                return "ERROR(\{e})"
            }
        }
    }
}