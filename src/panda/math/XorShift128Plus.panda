package panda.math

====================================================================================================
A fast pseudorandom number generator using a XorShift+ algorithm with a period of 2^128 - 1.

This generator is guaranteed to always produce the same random sequence when started from the same
seed, even across different environments and versions of Panda.
====================================================================================================
class XorShift128Plus {
    ================================================================================================
    The current state.
    ================================================================================================
    @private 
    var state1:Int64
    @private 
    var state2:Int64
    
    ================================================================================================
    Creates a new `XorShift128Plus` with a specific seed. All `XorShift128Plus` instances created
    with the same seed will return the same sequence of pseudorandom numbers.
    
    @param seed the seed to use
    ================================================================================================
    init(seed:Int64) {
        if seed = 0 {
            -- XorShift seed cannot be zero, replace with an arbitrarily-chosen value
            self.state1 := 12345678
        }
        else {
            self.state1 := seed
        }
        self.state2 := !!self.state1
    }
    
    @override
    method int64():Int64 {
        def x := state1
        var y := state2
        state1 := y
        x ~~= x << 23
        state2 := x ~~ y ~~ (x >> 17) ~~ (y >> 26)
        return state2 + y
    }

    method int(n:Int64):Int64 {
        if n = 1 {
            return 0
        }

        def min := -n % n
        loop {
            def x := int64()
            if x >= min {
                return x % n
            }
        }
    }
}