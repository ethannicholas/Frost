package frost.collections

====================================================================================================
An implementation of the set abstract data type. Sets are collections which cannot contain duplicate
entries; adding an element which already exists to a set does not do anything.
====================================================================================================
@final
class HashSet<T:HashKey<T>> : Collection<T> {
    @private
    def contents := HashMap<T, T>()

    ================================================================================================
    Creates a new empty set.
    ================================================================================================
    init() {
    }

    ================================================================================================
    Creates a new set containing all of the elements from another collection. Duplicate entries are
    filtered out, so

        -- testcase HashSetInit
        HashSet<Int>(list).count

    is an easy way to determine how many unique entries `list` contains.
    ================================================================================================
    init(c:CollectionView<T>) {
        addAll(c)
    }

    @override
    method add(value:T) {
        contents[value] := value
    }

    @override
    method addAll(c:CollectionView<T>) {
        for v in c {
            add(v)
        }
    }

    ================================================================================================
    Removes an entry from the set, if present.
    ================================================================================================
    method remove(value:T) {
        contents.remove(value)
    }

    @override
    function get_count():Int {
        return contents.count
    }

    @override
    method clear() {
        contents.clear()
    }

    @override
    function get_iterator():Iterator<T> {
        return contents.keys
    }

    ================================================================================================
    Returns `true` if the value is present in the set.
    ================================================================================================
    function contains(value:T):Bit {
        return contents.contains(value)
    }

    @override
    method filterInPlace(test:(T)=>(Bit)) {
        contents.filterInPlace((k, v) => test(k))
    }

    @override
    method mapInPlace(f:(T)=>(T)) {
        for (k, v) in contents.entries {
            contents[k] := f(v)     
        }
    }

    @override
    function get_toString():String {
        def result := MutableString("[")
        var separator := ""
        for v in contents.keys {
            result.append(separator)
            -- FIXME cast shouldn't be necessary
            if v->Object? !== null {
                result.append(v)
            }
            else {
                result.append("<null>")
            }
            separator := ", "
        }
        result.append("]")
        return result.finish()
    }
}
