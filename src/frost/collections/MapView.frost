package frost.collections

====================================================================================================
A read-only view of the standard "Map" abstract data type, which stores key/value pairs such that no
key appears more than once in the collection. Given a key, a `MapView` will return the corresponding
value, and generally does so very quickly (constant or `log n` time, depending on the
implementation).
====================================================================================================
interface MapView<K, V> {
    ================================================================================================
    The number of key/value pairs the map contains.
    ================================================================================================
    property count:Int

    ================================================================================================
    An iterator which returns all of the keys in the map.
    ================================================================================================
    property keys:Iterator<K>

    ================================================================================================
    An iterator which returns all of the values in the map. Because multiple keys may map to the
    same value, this iterator may return duplicate values.
    ================================================================================================
    property values:Iterator<V>

    ================================================================================================
    An iterator which returns all of the (key, value) pairs in the map.
    ================================================================================================
    property entries:Iterator<(K, V)>

    function get_count():Int

    ================================================================================================
    Returns the value mapped to the given key, or `null` if no such mapping exists. If the map's
    type permits `null` values, this function does not distinguish between cases where the map does
    not contain the given key, and cases where it does contain the key, but it maps to `null`. The
    `contains` method can be used to disambiguate this situation.
    ================================================================================================
    function [](key:K):V?

    ================================================================================================
    Returns `true` if the map contains the given key (even if the value it maps to is `null`)
    ================================================================================================
    function contains(key:K):Bit

    function get_entries():Iterator<(K, V)>

    @default
    function get_keys():Iterator<K> {
        -- FIXME inference
        return entries.map<K>(e => e[0])
    }

    @default
    function get_values():Iterator<V> {
        -- FIXME inference
        return entries.map<V>(e => e[1])
    }
}