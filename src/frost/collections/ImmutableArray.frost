package frost.collections

uses frost.unsafe.Pointer

@final
class ImmutableArray<T:Immutable> : Immutable, ListView<T> {
    @private
    class ImmutableArrayIterator<T:Immutable> : Iterator<T> {
        var array:ImmutableArray<T>

        var index := 0

        init(array:ImmutableArray<T>) {
            self.array := array
        }

        @override
        function get_done():Bit {
            return index = array.count
        }

        @override
        function next():T {
            index += 1
            return array[index - 1]
        }
    }

    @private
    def _count:Int

    @private
    def dummy:Int -- for binary compatibility with Array

    @private
    def data:Pointer<T?>

    init() {
        self._count := 0
        data := Pointer<T?>.alloc(0)
    }

    init(data:ListView<T>) {
        self._count := data.count
        self.data := Pointer<T?>.alloc(_count)
        for i in 0 .. count {
            self.data[i] := data[i]
        }
    }

    @private
    init(data:Pointer<T>, count:Int) {
        self.data := data->Pointer<T?>
        self._count := count
    }

    ================================================================================================
    Invalidates an `Array` and returns its contents as an `ImmutableArray`. This is generally
    preferable to creating a new instance of `ImmutableArray` from this array, as `finish()` does
    not copy the array's contents. Interacting in any way with an `Array` after calling this method
    on it will cause precondition violations (or, if safety checks are disabled, undefined
    behavior).
    ================================================================================================
    @class
    method from(a:Array<T>):ImmutableArray<T> {
        -- FIXME this transformation is only safe at -S0
        a.data := a.data.realloc(a.capacity, a._count)
        a.$class := ImmutableArray<T>().$class -- FIXME need a better way to get a class Pointer
        a.capacity := -1
        return a->Object->ImmutableArray<T>
    }

    @override
    method cleanup() {
        for i in 0 .. count {
            data[i] := null
        }
        data.destroy()
    }

    @override
    function [](index:Int):T {
        assert index >= 0 & index < count
        return data[index]->T
    }

    @override
    function get_count():Int {
        return _count
    }

    @override
    function get_iterator():Iterator<T> {
        return ImmutableArrayIterator<T>(self)
    }

    @override
    function get_toString():String {
        def result := MutableString()
        result.append("[")
        var separator := ""
        for v in self {
            result.append(separator)
            separator := ", "
            -- FIXME cast shouldn't be necessary
            if v->Object? !== null {
                result.append(v)
            }
            else {
                result.append("<null>")
            }
        }
        result.append("]")
        return result.finish()
    }
}
