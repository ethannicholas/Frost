package frost.io

====================================================================================================
Provides access to the standard input, output, and error streams.
====================================================================================================
class Console {
    @private
    init() {
    }

    ================================================================================================
    Returns the standard input stream.
    ================================================================================================
    @class
    @external
    function inputStream():InputStream

    ================================================================================================
    Returns the standard output stream.
    ================================================================================================
    @class
    @external
    function outputStream():OutputStream

    ================================================================================================
    Returns the standard error stream.
    ================================================================================================
    @class
    @external
    function errorStream():OutputStream

    ================================================================================================
    Prints a string to the standard output stream.
    ================================================================================================
    @class
    @external
    method print(s:String)

    ================================================================================================
    Prints a string followed by a newline to the standard output stream.
    ================================================================================================
    @class
    method printLine(s:String) {
        print(s)
        printLine()
    }

    ================================================================================================
    Prints the string represention of an object to the standard output stream.
    ================================================================================================
    @class
    method print(o:Object) {
        print(o.toString)
    }

    ================================================================================================
    Prints the string represention of an object followed by a newline to the standard output stream.
    ================================================================================================
    @class
    method printLine(o:Object) {
        printLine(o.toString)
    }

    ================================================================================================
    Prints a newline to the standard output stream.
    ================================================================================================
    @class
    method printLine() {
        print("\n")
    }

    ================================================================================================
    Prints a string to the standard error stream.
    ================================================================================================
    @class
    @external
    method printError(s:String)

    ================================================================================================
    Prints a string followed by a newline to the standard error stream.
    ================================================================================================
    @class
    method printErrorLine(s:String) {
        printError(s)
        printErrorLine()
    }

    ================================================================================================
    Prints the string represention of an object to the standard error stream.
    ================================================================================================
    @class
    method printError(o:Object) {
        printError(o.toString)
    }

    ================================================================================================
    Prints the string represention of an object followed by a newline to the standard error stream.
    ================================================================================================
    @class
    method printErrorLine(o:Object) {
        printErrorLine(o.toString)
    }

    ================================================================================================
    Prints a newline to the standard error stream.
    ================================================================================================
    @class
    method printErrorLine() {
        printError("\n")
    }

    ================================================================================================
    Reads a single byte from the standard input stream. Returns `null` if the end of the stream has
    been reached.
    ================================================================================================
    @class
    @external
    method read():Char8?

    ================================================================================================
    Reads a line of text from the standard input stream. Returns `null` if the end of the stream has
    been reached.
    ================================================================================================
    @class
    method readLine():String? {
        def result := MutableString()
        loop {
            def c:Char8? := read()
            if c == null {
                if result.byteLength = 0 {
                    return null
                }
                break
            }
            if c = "\n" {
                break
            }
            result.append(c)
        }
        return result.finish()
    }
}