method main(args:ListView<String>) {
    try {
        if args.count < 2 | !args[1].endsWith(".frost") {
            Console.errorStream().printLine("Usage: frost <Script.frost> <args...>")
            System.exit(1)
        }
        def frostc := File(args[0]).parent.resolve("frostc")
        def out := System.temporaryDirectory().resolve("frost-tmp")
        def frostcProcess := System.exec(frostc.path, [args[1], "-o", out.convert()])
        Thread.start(method() { def ignored := Console.inputStream().sendTo(frostcProcess.standardInput()) })
        Thread.start(method() { def ignored := frostcProcess.standardOutput().sendTo(Console.outputStream()) })
        Thread.start(method() { def ignored := frostcProcess.standardError().sendTo(Console.errorStream()) })
        if frostcProcess.waitFor() = 0 {
            def process := System.exec(out.path, args[2..])
            Thread.start(method() { def ignored := Console.inputStream().sendTo(process.standardInput()) })
            Thread.start(method() { def ignored := process.standardOutput().sendTo(Console.outputStream()) })
            Thread.start(method() { def ignored := process.standardError().sendTo(Console.errorStream()) })
            def exitCode := process.waitFor()
            System.exit(exitCode)
        }
    }
    fail(error) {
        Console.printErrorLine(error)
    }
    System.exit(1)
}