method main(args:ListView<String>) {
    try {
        Console.printLine("STARTUP")
        if args.count < 2 | !args[1].endsWith(".frost") {
            Console.errorStream().printLine("Usage: frost <Script.frost> <args...>")
            System.exit(1)
        }
        def frostc := File(args[0]).parent.resolve("frostc")
        def out := System.temporaryDirectory().resolve("frost-tmp")
        def frostcProcess := System.exec(frostc.path, [args[1], "-o", out.convert()])
        Thread.start(method() { Console.inputStream().sendTo(frostcProcess.standardInput())! })
        Thread.start(method() { frostcProcess.standardOutput().sendTo(Console.outputStream())! })
        Thread.start(method() { frostcProcess.standardError().sendTo(Console.errorStream())! })
        if frostcProcess.waitFor() = 0 {
            Console.printLine("success!")
            def process := System.exec(out.path, args[2..])
            Thread.start(method() { Console.inputStream().sendTo(process.standardInput())! })
            Thread.start(method() { process.standardOutput().sendTo(Console.outputStream())! })
            Thread.start(method() { process.standardError().sendTo(Console.errorStream())! })
            def exitCode := process.waitFor()
            Console.printLine("done \{exitCode}!")
            System.exit(exitCode)
        }
        else {
            Console.printLine("compile error")
        }
    }
    fail(error) {
        Console.printErrorLine(error)
    }
    Console.printLine("failed")
    System.exit(1)
}