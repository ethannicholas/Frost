choice Kind {
    NORMAL
    EQUATABLE
    KEY
}

method writeTuple(count:Int, kind:Kind, path:File) {
    try {
        def out := path.openOutputStream()
        out.printLine("package frost.core")
        out.printLine("===@hidden===")
        def name:String
        match kind {
            when Kind.NORMAL    { name := "Tuple\{count}" }
            when Kind.EQUATABLE { name := "EquatableTuple\{count}" }
            when Kind.KEY       { name := "KeyTuple\{count}" }
        }
        out.print("class \{name}<")
        for i in Int(0) .. count {
            if i > 0 {
                out.print(", ")
            }
            out.print("T\{i}")
            match kind {
                when Kind.EQUATABLE { out.print(":Equatable<T\{i}>")}
                when Kind.KEY { out.print(":HashKey<T\{i}>")}
            }
        }
        out.print(">")
        def types := Int[0 .. count].map(i => "T\{i}").join(", ")
        match kind {
            when Kind.EQUATABLE {
                out.print(" : Equatable<(\{types})>")
            }
            when Kind.KEY {
                out.print(" : HashKey<(\{types})>")
            }
        }
        out.printLine(" {")
        for i in Int(0) .. count {
            out.printLine("    def f\{i}:T\{i}")
        }
        out.printLine()
        out.printLine("    init(\{Int[0 .. count].map(i => "f\{i}:T\{i}").join(", ")}) {")
        for i in Int(0) .. count {
            out.printLine("       self.f\{i} := f\{i}")
        }
        out.printLine("    }")
        out.printLine()
        out.printLine("    @pre(i >= 0 & i < \{count})")
        out.printLine("    function []<T>(i:Int):T {")
        out.printLine("        match i {")
        for i in Int(0) .. count {
            out.printLine("            when \{i} { return f\{i}->T }")
        }
        out.printLine("        }")
        out.printLine("        unreachable")
        out.printLine("    }")
        out.printLine()
        match kind {
            when Kind.EQUATABLE, Kind.KEY {
                out.printLine("    @override")
                out.printLine("    function =(other:(\{types})):Bit {")
                for i in Int(0) .. count {
                    out.printLine("        if f\{i} != other.f\{i} { return false }")
                }
                out.printLine("        return true")
                out.printLine("    }")
            }
        }
        if kind = Kind.KEY {
            out.printLine("    @override")
            out.printLine("    function get_hash():Int {")
            out.printLine("        var result := 0")
            for i in Int(0) .. count {
                out.printLine("        result ~~= f\{i}.hash *& \{[1, 73, 179, 283, 419, 547, 661,
                        811, 947, 1087, 1229, 1381, 1523, 1663, 1823, 1993][i]}")
            }
            out.printLine("        return result")
            out.printLine("    }")
        }
        out.printLine("    @override")
        out.printLine("    function get_toString():String {")
        out.printLine("        return \"(\{Int[0 .. count].map(i => "\\{f\{i}}").join(", ")})\"")
        out.printLine("    }")
        out.printLine("}")
    }
    fail(error) {
        Console.printErrorLine(error)
        System.exit(1)
    }
}

method main() {
    def corePath := File("../src/frost/core")
    for i:Int in 2 ... 16 {
        writeTuple(i, Kind.NORMAL,    corePath.resolve("Tuple\{i}.frost"))
        writeTuple(i, Kind.EQUATABLE, corePath.resolve("EquatableTuple\{i}.frost"))
        writeTuple(i, Kind.KEY,       corePath.resolve("KeyTuple\{i}.frost"))
    }
}
