package org.frostlang.plex.runtime

uses frost.unsafe.NewPointer

@final
class DFA {
    constant START_CHAR:UInt8 := 0
    constant END_CHAR:UInt8 := 255

    def stateCount:Int

    def transitions:NewPointer<NewPointer<Int>>

    def accepts:NewPointer<Int>

    var source:NewPointer<Char8>

    var offset:Int

    var sourceEnd:Int

    var line := 1

    var column := 1

    init(stateCount:Int, transitions:NewPointer<NewPointer<Int>>, accepts:NewPointer<Int>) {
        init("", stateCount, transitions, accepts)
    }

    init(source:String, stateCount:Int, transitions:NewPointer<NewPointer<Int>>,
            accepts:NewPointer<Int>) {
        self.source := source.data
        self.offset := 0
        self.sourceEnd := source.byteLength
        self.stateCount := stateCount
        self.transitions := transitions
        self.accepts := accepts
    }

    method next():RawToken {
        var state := 1
        if offset = sourceEnd {
            return RawToken(0, String.Index(offset), String.Index(offset), line, column)
        }
        var kind := -1
        def start := offset
        var end := offset
        def startLine := line
        def startColumn := column
        while offset != sourceEnd {
            def c := source[offset]
            if c.asUInt8 > END_CHAR {
                Console.printLine("invalid token \{c.asUInt8}")
                System.exit(1)
            }
            state := transitions[c.asInt][state]
            if state != 0 {
                offset += 1
                match c.asUInt8 {
                    when 10 {
                        line += 1
                        column := 1
                    }
                    when 9 {
                        column += 4->Int - column % 4
                    }
                    otherwise {
                        column += 1
                    }
                }
                def newAccept := accepts[state]
                if newAccept != -1 {
                    kind := newAccept
                    end := offset
                }
            }
            else {
                break
            }
        }
        offset := end
        return RawToken(kind, String.Index(start), String.Index(end), startLine, startColumn)
    }
}